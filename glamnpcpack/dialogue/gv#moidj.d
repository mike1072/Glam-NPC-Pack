BEGIN GV#MOIDJ

//Silke
I_C_T ~%tutu_var%SILKE~ 6 GV#MOIDInterjectSILKE1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5532 /* Well isn't this just the story of my life. */
END

//Hurgan
I_C_T ~%tutu_var%HURGAN~ 4 GV#MOIDInterjectHURGAN1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5533 /* Know the name. Know the tower too. Hope this isn't going where I think it is. */
END

I_C_T ~%tutu_var%HURGAN~ 7 GV#MOIDInterjectHURGAN2
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5534 /* Any answer but "Gods no" and you'll be making my job real difficult. */
END

//Jenkal
I_C_T2 ~%tutu_var%JENKAL~ 0 GV#MOIDInterjectJENKAL1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5535 /* Least somebody here has some sense. */
END

//Kaishas
I_C_T2 ~%tutu_var%KAISH~ 13 GV#MOIDInterjectKAISH1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5536 /* Don't be too eager to escape. Place like this, deaf to the world's troubles...it's not the prison you think it is. */
END

//Captain Tollar
I_C_T2 ~%tutu_var%KIERES~ 7 GV#MOIDInterjectKIERES1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5537 /* Huh. Either we've met a lightweight sailor or that's one hell of a brew. Might have to pick some up myself. For research. */
END

//Ghost Knights
I_C_T2 ~%GhostKnight%~ 0 GV#MOIDInterjectKNIGHT1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5538 /* There's the power of an oath for you. Simple words, but it keeps them anchored, spitting in death's face. */
END

I_C_T2 ~%GhostKnight%~ 1 GV#MOIDInterjectKNIGHT2
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5539 /* Tan am sonn. */
END

//Ted
I_C_T2 ~%tutu_var%HUNTER~ 1 GV#MOIDInterjectHUNTER1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5540 /* Sane people quit after something like that. Explains the shortage of sane adventurers. */
END

//Lamalha
I_C_T2 ~%tutu_var%LAMALH~ 0 GV#MOIDInterjectLAMALH1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5541 /* Somehow got the feeling it doesn't. */
END

//Laskal
I_C_T2 ~%tutu_var%LASKAL~ 3 GV#MOIDInterjectLASKAL1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5542 /* Doesn't look like a courier to me. Answer carefully. */
END

//Lussel
I_C_T2 ~%tutu_var%LUSSEL~ 2 GV#MOIDInterjectLUSSEL1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5543 /* Ooze booze? Knew humans couldn't brew worth a damn, but I didn't realise it was this bad. */
END

//Marek
I_C_T2 ~%tutu_var%MAREK~ 3 GV#MOIDInterjectMAREK1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5544 /* Heard it before, every which way, from worse and from better. Still kicking. */
END

//Meilum
I_C_T2 ~%tutu_var%MEILUM~ 0 GV#MOIDInterjectMEILUM1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5545 /* If you have to introduce yourself as the Coast's greatest swordsman, you aren't. */
END

//Molkar
I_C_T2 ~%tutu_var%MOLKAR~ 0 GV#MOIDInterjectMOLKAR1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5546 /* Strange how they all say that. A "competent" bounty hunter would notice the pattern. */
END

//Neira
I_C_T ~%tutu_var%NEIRA~ 0 GV#MOIDInterjectNEIRA1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5547 /* Might've been easy coins waiting for us to sleep. Now I don't like your chances. */
END

//Neville
I_C_T ~%tutu_var%NEVILL~ 0 GV#MOIDInterjectNEVILLE1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5548 /* Grab some shade. Archers lurking. */
END

//Niklos
I_C_T ~%tutu_var%NIKLOS~ 1 GV#MOIDInterjectNIKLOS1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5549 /* Careful. Someone pays gold just to talk to you, they mean to get it back.  */
END

//Scar
I_C_T ~%tutu_var%SCAR~ 2 GV#MOIDInterjectSCAR1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5550 /* Only capacity I ever work in. */
END

//Dorotea


//Stephan
I_C_T2 ~%tutu_var%STEPHA~ 4 GV#MOIDInterjectSTEPHAN1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5551 /* Easiest interrogation I've ever seen. If everyone folded that quick, there'd be a lot less busted knees in the world. */
END

//Surgeon
I_C_T2 ~%tutu_var%SURGEO~ 5 GV#MOIDInterjectSURGEON1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5552 /* Healing in penance. Might've done the same myself, on better terms with the gods. */
END

//Tamoko
I_C_T2 ~%tutu_var%TAMOKO~ 10 GV#MOIDInterjectTAMOKO1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5553 /* Don't promise before you know. This is not a road you want to walk. */
END

//Alatos
I_C_T ~%tutu_var%ALATOS~ 0 GV#MOIDInterjectALATOS1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5554 /* Exactly what someone would say before commencing hostilities. */
END

//Tarnesh
I_C_T ~%tutu_var%TARNES~ 0 GV#MOIDInterjectTARNESH1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5555 /* Guard up. This one's too chummy. */
END

//Nalin
I_C_T2 ~%tutu_var%NALIN~ 4 GV#MOIDInterjectNALIN1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5556 /* Gallows would've been kinder to him. You don't come back from something like this. */
END

//Wenric
I_C_T2 ~%tutu_var%WENRIC~ 1 GV#MOIDInterjectWENRIC1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
@5557 /* Bag twenty corpses, keep my own count, 150 gold. All for a peaceful night. Wish more clients were this simple. */
END



APPEND GV#MOIDJ


//Friendship Track

//Talk 1

IF ~Global("GV#MOIDTalk","GLOBAL",2)~ t
SAY @5558 /* Big job, guarding you. Wonder sometimes if I'm up to it. */
++ @5559 /* You've done fine work so far. */ + t.1
++ @5560 /* So do I. */ + t.2
++ @5561 /* Surely you've had more important clients than me. */ + t.3
++ @5562 /* Didn't I say I don't need protection? You're just here so we can make shorter work of my enemies. */ + t.4
++ @5563 /* It'd be a smaller job with less talk. Be quiet. */ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) SetGlobal("GV#MOIDShutUp","GLOBAL",1)~ EXIT
END

IF ~~ t.1
SAY @5564 /* "So far" doesn't mean anything. You only did fine when it's over. */
IF ~~ + t.5
END

IF ~~ t.2
SAY @5565 /* Good thing you didn't pay in advance. Or at all, really. */
IF ~~ + t.5
END

IF ~~ t.3
SAY @5566 /* Might've done. Might've wondered then, too. */
IF ~~ + t.5
END

IF ~~ t.4
SAY @5567 /* That attitude's what makes it a big job. */
IF ~~ + t.5
END

IF ~~ t.5
SAY @5568 /* Been working one-offs for a while now. Smugglers. Cons who con the wrong people. You want to keep your business dark, you don't hire long-term. This is a jump. */
++ @5569 /* You take jobs with criminals? */ + t.6
++ @5570 /* You could do better than that. */ + t.7
++ @5571 /* I hadn't pegged you for the type. */ + t.8
++ @5572 /* Shady dealings, eh? I knew there was more to you. */ + t.9
END

IF ~~ t.6
SAY @5573 /* They don't say they're criminals when they hire me. */
IF ~~ + t.10
END

IF ~~ t.7
SAY @5574 /* No I can't. Tried it. */
IF ~~ + t.10
END

IF ~~ t.8
SAY @5575 /* Only thing I'm good at. Not much room to fuss. */
IF ~~ + t.10
END

IF ~~ t.9
SAY @5576 /* There isn't. I don't get into devilry. I just take lumps for those who do. */
IF ~~ + t.10
END

IF ~~ t.10
SAY @5577 /* What did you expect? People who deserve bodyguards don't pick them up in taverns. */
+ ~ReputationGT(Player1,12)~ + @5578 /* I picked you up in a tavern. */ + t.11
+ ~ReputationLT(Player1,13)~ + @5578 /* I picked you up in a tavern. */ + t.12
++ @5579 /* Don't offer your services in taverns, then! */ + t.13
++ @5580 /* I resent that. */ + t.14
++ @5581 /* Then why not stop working for them? */ + t.15
END

IF ~~ t.11
SAY @5582 /* Bad people put up good fronts. */
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600) ~ EXIT
END

IF ~~ t.12
SAY @5583 /* And what? You're an exception? Haven't done much to convince me. */
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600) ~ EXIT
END

IF ~~ t.13
SAY @5584 /* I'll rephrase. People who deserve bodyguards don't pick me. */
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600) ~ EXIT
END

IF ~~ t.14
SAY @5585 /* Resent away. Truth's truth. */
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600) ~ EXIT
END


IF ~~ t.15
SAY @5586 /* And starve to death? Thought about it. Still do. */
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600) ~ EXIT
END


//Talk 2

IF ~Global("GV#MOIDTalk","GLOBAL",4)~ t1
SAY @5587 /* Someone teach you to fight, or did you just pick it up as you went? */
+ ~!Class(Player1,FIGHTER_ALL) !Class(Player1,RANGER_ALL) !Class(Player1,PALADIN_ALL)~ + @5588 /* I wouldn't exactly call myself a fighter. */ + t1.1
++ @5589 /* I took lessons with the guards in Candlekeep. */ + t1.2
++ @5590 /* The latter. I've had to pick lots of things up lately. */ + t1.3
++ @5591 /* Neither. It's just a gift. */ + t1.4
END

IF ~~ t1.1
SAY @5592 /* More to it than hitting people. Acting fast, keeping your wits, positioning yourself. Takes a fighter's brain to handle that.  */
IF ~~ + t1.5
END

IF ~~ t1.2
SAY @5593 /* Think you mostly picked it up, then. No offence to them, but I doubt it takes much to guard books. */
IF ~~ + t1.5
END

IF ~~ t1.3
SAY @5594 /* Sink or swim. Best way. No substitute for the real thing, risk aside. */
IF ~~ + t1.5
END

IF ~~ t1.4
SAY @5595 /* Raw talent, is it? Had me fooled. Or you're just full of crap. */
IF ~~ + t1.5
END

IF ~~ t1.5
SAY @5596 /* Not commenting on your skills, mind. Just wondering where normal people learn to do this. */
++ @5597 /* What do you mean? */ + t1.6
++ @5598 /* I'm hardly normal, Moidre. */ + t1.6
++ @5599 /* So you're an abnormal person? */ + t1.6
END

IF ~~ t1.6
SAY @5600 /* You're not a soldier. That's what I mean. You weren't... */
= @5601 /* You're not a soldier. */
++ @5602 /* Are you? */ + t1.7
++ @5603 /* I wasn't what? */ + t1.7
++ @5604 /* Soldiers aren't the only ones who can kill. */ + t1.7
END

IF ~~ t1.7
SAY @5605 /* Never mind. Shouldn't be chatting anyway. Ignore me. */
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600) ~ EXIT
END



// Talk 3

IF ~Global("GV#MOIDTalk","GLOBAL",6)~ t2
SAY @2696 /* Zzz... */
= @5606 /* Hng...huh?! */
++ @5607 /* Moidre?  */ + t2.1
++ @5608 /* Keep it down over there! */ + t2.1
++ @5609 /* Did you see something? */ + t2.1
END

IF ~~ t2.1
SAY @5610 /* Oh. Sorry. Fell asleep. */
++ @5611 /* Why are you sorry? We're supposed to be sleeping. */ + t2.2
++ @5612 /* Still standing guard, are you? */ + t2.3
++ @5613 /* Don't let it happen again. */ + t2.4
++ @5614 /* Are you saying you don't sleep? */ + t2.5
END

IF ~~ t2.2
SAY @5531 /* I'm not. */
IF ~~ + t2.6
END

IF ~~ t2.3
SAY @5615 /* Maybe not "standing" guard. */
IF ~~ + t2.6
END

IF ~~ t2.4
SAY @5616 /* Do my best. */
IF ~~ + t2.6
END

IF ~~ t2.5
SAY @5617 /* Not on purpose. Not when I'm working. */
IF ~~ + t2.6
END

IF ~~ t2.6
SAY @5618 /* You've done some tossing and turning yourself lately. Nightmares? */
++ @5619 /* Yes. About my foster father's death and...something else. A voice. */ + t2.7
++ @5620 /* Not exactly. Just strange dreams. Vivid ones. */ + t2.8
+ ~!Race(Player1,ELF)~ + @5621 /* No. I'm just a restless sleeper. */ + t2.9
+ ~Race(Player1,ELF)~ + @5622 /* No. Some elves are just restless during trances. */ + t2.9
++ @5623 /* Goodnight, Moidre. */ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600) RestParty()~ EXIT
END

IF ~~ t2.7
SAY @5624 /* Wouldn't worry. Someone close dies, it's natural they'll linger. And hearing voices is only madness when you're awake. */
IF ~~ + t2.10
END

IF ~~ t2.8
SAY @5625 /* Better strange than grim. Wouldn't fault you for sleeping the day away, if strange is the worst you can say. */
IF ~~ + t2.10
END

IF ~~ t2.9
SAY @5626 /* Restless is right. Roll around so much I near have to drag you back to camp. */
IF ~~ + t2.10
END

IF ~~ t2.10
SAY @5627 /* I used to have dreams. Proper dreams, the sort you'd tell people about. Can't remember them anymore, but I know I had them. */
= @5628 /* These days I just dream of work. Go to sleep, do a day's toil in my head, wake up ready for bed. Don't know why I bother. */
++ @5629 /* Sounds like you're tired of your job. Why not do something else? */ + t2.11
++ @5630 /* Bother sleeping, or bother in general? */ + t2.12
++ @5631 /* I wouldn't rest either if I didn't have to. */  + t2.13
++ @5632 /* I know why I bother. Goodnight. */ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600) RestParty()~ EXIT
END

IF ~~ t2.11
SAY @5633 /* Ever get tired of adventuring? */
++ @1376 /* Yes. */ + t2.14
++ @381 /* No. */ + t2.15
END

IF ~~ t2.12
SAY @5634 /* Either. */
IF ~~ + t2.16
END

IF ~~ t2.13
SAY @5635 /* Gods' way of keeping us in check. Imagine how fast we could ruin the world if we didn't have to stop and lie down sometimes. */
IF ~~ + t2.16
END

IF ~~ t2.14
SAY @5636 /* But you're still doing it. */
IF ~~ + t2.16
END

IF ~~ t2.15
SAY @5637 /* You wouldn't understand, then. */
IF ~~ + t2.16
END

IF ~~ t2.16
SAY @5638 /* Anyway. Up now. Think I'll just stay up. Sleep safe. */
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTImer","GLOBAL",3600) RestParty()~ EXIT
END


// Talk 4


IF ~Global("GV#MOIDTalk","GLOBAL",8)~ t3
SAY @5639 /* (Let me suffer where you would have suffered. Let me bleed where you would have bled. Let me die where you would have died. My shield is my self...) */
++ @5640 /* What are you doing? */ + t3.1
++ @5641 /* (ignore her) */ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTImer","GLOBAL",3600)~ EXIT
END

IF ~~ t3.1
SAY @5642 /* The oath. Have to recite it every day, even just to myself. Suppose you were bound to notice eventually. */
++ @5643 /* You "have" to recite it? */ + t3.2
++ @5644 /* That doesn't sound like mercenary training to me. */ + t3.3
++ @5645 /* Where did that oath come from? Surely you didn't just make it up. */ + t3.4
END

IF ~~ t3.2
SAY @5646 /* Well. Don't *have* to. But I do. */
IF ~~ + t3.5
END

IF ~~ t3.3
SAY @5647 /* Know a lot of mercenaries? */
IF ~~ + t3.5
END

IF ~~ t3.4
SAY @5648 /* Why not? Someone has to make them up. */
IF ~~ + t3.5
END

IF ~~ t3.5
SAY @5649 /* It's difficult, the world. Everyone's uncertain sometimes. Some more than others. Helps to have something to go by when hesitation can kill you. */
++ @5650 /* Most people would go by their morals. */ + t3.6
++ @5651 /* You mustn't have much confidence in it, if you have to keep repeating it to yourself. */ + t3.7
++ @5652 /* I say a person should trust their instincts. */ + t3.8
++ @5653 /* I've never hesitated in my life. */ + t3.9
END

IF ~~ t3.6
SAY @5654 /* Morals need a foundation. Otherwise they're just feelings. */
IF ~~ + t3.10
END

IF ~~ t3.7
SAY @5655 /* Priests say their prayers every day. Doesn't mean they don't believe. */
IF ~~ + t3.10
END

IF ~~ t3.8
SAY @5656 /* Instinct tells you to flinch when you should follow through. Instinct tells you to rest when you need to press on. It's the last thing a person should trust. */
IF ~~ + t3.10
END

IF ~~ t3.9
SAY @5657 /* Live longer. Then that'll be something to boast about. */
IF ~~ + t3.10
END

IF ~~ t3.10
SAY @5658 /* Tan am sonn. "Be as stone." Oldest dwarven saying. Not just a saying; a fond wish, a farewell. A battlecry and a wedding vow. A drinking song and a hymn. It's everything. */
= @5659 /* Oaths are stone. Keep to them, and they'll brace you against the world. Walk away and they'll loom at your back forever. But they stand, with or without you. */
++ @5660 /* So as long as you keep your oaths, no matter what it takes, it means you're a good person? */ + t3.11
++ @5661 /* I agree. A person's word is the only thing that matters. */ + t3.12
++ @5662 /* I'd sooner betray an oath than betray myself. */ + t3.13
++ @5663 /* What a load of nonsense. */ + t3.14
END

IF ~~ t3.11
SAY @5664 /* It means you're stone. Sometimes that's all a person can be. */
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTImer","GLOBAL",3600)~ EXIT
END

IF ~~ t3.12
SAY @5665 /* Then you don't agree, because that's not what I said. */
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTImer","GLOBAL",3600)~ EXIT
END

IF ~~ t3.13
SAY @5666 /* You decided you're worth more. Some people didn't. */
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTImer","GLOBAL",3600)~ EXIT
END

IF ~~ t3.14
SAY @5667 /* Maybe. Best I've got, though. */
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTImer","GLOBAL",3600)~ EXIT
END
END


//Talk 5
CHAIN IF ~Global("GV#MOIDTalk","GLOBAL",10) Global("GV#MOIDHappy","GLOBAL",1)~ THEN GV#MOIDJ t4
@5668 /* What's that human saying? "History repeats itself"? It really does. */
== GV#MOIDJ IF ~GlobalLT("CHAPTER","GLOBAL",%tutu_chapter_7%)~ THEN @5669 /* Everyone in Baldur's Gate thinks this Rieltar is their saviour. Swooping in with his iron when all hope seemed lost, arming them for a war they'd have died in otherwise. */
== GV#MOIDJ IF ~GlobalGT("CHAPTER","GLOBAL",%tutu_chapter_6%)~ THEN @5670 /* Everyone in Baldur's Gate thinks Sarevok is their saviour. Swooping in with his iron when all hope seemed lost, arming them for a war they'd have died in otherwise. */
== GV#MOIDJ @5671 /* Every one of them is wrong. And if I told them, not a single one would listen. */
END
+ ~GlobalLT("CHAPTER","GLOBAL",%tutu_chapter_7%)~ + @5672 /* And that's history repeating itself? */ EXTERN GV#MOIDJ t4.1
+ ~GlobalGT("CHAPTER","GLOBAL",%tutu_chapter_6%)~ + @5672 /* And that's history repeating itself? */ EXTERN GV#MOIDJ t4.2
+ ~GlobalLT("CHAPTER","GLOBAL",%tutu_chapter_7%)~ + @5673 /* I'm not sure what you mean. */ EXTERN GV#MOIDJ t4.3
+ ~GlobalGT("CHAPTER","GLOBAL",%tutu_chapter_6%)~ + @5673 /* I'm not sure what you mean. */ EXTERN GV#MOIDJ t4.4
+ ~GlobalLT("CHAPTER","GLOBAL",%tutu_chapter_7%)~ + @5674 /* Don't be too upset. They wouldn't listen to me either. */ EXTERN GV#MOIDJ t4.3
+ ~GlobalGT("CHAPTER","GLOBAL",%tutu_chapter_6%)~ + @5674 /* Don't be too upset. They wouldn't listen to me either. */ EXTERN GV#MOIDJ t4.4


CHAIN GV#MOIDJ t4.1
@5675 /* For me and for the world. Pick a date, pick a place, somewhere you'll find a Rieltar. Just so happens this is my second. */
EXTERN GV#MOIDJ t4.3

CHAIN GV#MOIDJ t4.2
@5676 /* For me and for the world. Pick a date, pick a place, somewhere you'll find a Sarevok. Just so happens this is my second. */
EXTERN GV#MOIDJ t4.4

CHAIN GV#MOIDJ t4.3
@5677 /* You've wondered where I came from? Who I used to work for? Look at Rieltar. There's your answer, more or less. */
END
++ @5678 /* You worked for Rieltar?! */ EXTERN GV#MOIDJ t4.5
++ @5679 /* Can you elaborate? */ EXTERN GV#MOIDJ t4.6
++ @5680 /* So you've been in this position before. */ EXTERN GV#MOIDJ t4.6

CHAIN GV#MOIDJ t4.4
@5681 /* You've wondered where I came from? Who I used to work for? Look at Sarevok. There's your answer, more or less. */
END
++ @5682 /* You worked for Sarevok?! */ EXTERN GV#MOIDJ t4.5
++ @5679 /* Can you elaborate? */ EXTERN GV#MOIDJ t4.6
++ @5680 /* So you've been in this position before. */ EXTERN GV#MOIDJ t4.6

CHAIN GV#MOIDJ t4.5
@5683 /* *sigh* You're dense sometimes. */
EXTERN GV#MOIDJ t4.6

CHAIN GV#MOIDJ t4.6
@5684 /* I was kuldarsamman to my clan leader. "Trusted warrior." Some humans call us dwarven defenders. */
= @5685 /* Hand-picked as a child, trained day and night to be a living wall. Body and mind. "My shield is my self, till my days are done." */
END
++ @5686 /* From childhood? That sounds like an awful life. */ EXTERN GV#MOIDJ t4.7
++ @5687 /* That explains a lot. */ EXTERN GV#MOIDJ t4.7
++ @5688 /* Hand-picked as a child? What, did they notice you clubbing the other kids with a stick? */ EXTERN GV#MOIDJ t4.8

CHAIN GV#MOIDJ t4.7
@5689 /* It's the greatest honour a mortal dwarf can give. Especially for...for me. More than I deserved. */
= @5690 /* Never in a million years thought anyone would envy me. Tried to be humble about it, but I'd watch the peasants as we marched from the keep, see the way they looked at me and... */
= @5691 /* Well. That's moot. Once you start seeing people as "peasants", it's already gone to your head. */
END
++ @5692 /* You mean you had an ego? I'm not sure I believe this story. */ EXTERN GV#MOIDJ t4.9
++ @5693 /* I knew you weren't just a mercenary. */ EXTERN GV#MOIDJ t4.10
+ ~GlobalLT("CHAPTER","GLOBAL",%tutu_chapter_7%)~ + @5694 /* So how was your clan leader like Rieltar? */ EXTERN GV#MOIDJ t4.11
+ ~GlobalGT("CHAPTER","GLOBAL",%tutu_chapter_6%)~ + @5695 /* So how was your clan leader like Sarevok? */ EXTERN GV#MOIDJ t4.11

CHAIN GV#MOIDJ t4.8
@5696 /* Noticed the other kids clubbing me, maybe. Noticed I didn't flinch much. */
EXTERN GV#MOIDJ t4.7

CHAIN GV#MOIDJ t4.9
@5697 /* Don't know if I'd go that far. I was proud of my place, not of myself. */
EXTERN GV#MOIDJ t4.12

CHAIN GV#MOIDJ t4.10
@5698 /* I was, looking back. A glorified one, a well-trained one, but still. */
EXTERN GV#MOIDJ t4.12

CHAIN GV#MOIDJ t4.11
@5699 /* It's a story. And I'm not much of a teller. Do you really want to hear it? */
END
++ @2728 /* I am. */ EXTERN GV#MOIDJ t4.13
++ @5700 /* The mysterious Moidre finally wants to shed some light on herself! As if I'd turn that down. */ EXTERN GV#MOIDJ t4.13
++ @5701 /* Nope! Just lost all interest. */ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) SetGlobal("GV#MOIDShutUp","GLOBAL",1)~ EXIT

CHAIN GV#MOIDJ t4.12
@5702 /* <CHARNAME>, this whole thing, my clan, my home... */
EXTERN GV#MOIDJ t4.11

CHAIN GV#MOIDJ t4.13
@5703 /* We...the clan...we were recovering from war. Duergar. They lost, but so did we. Thousands dead and nothing to show for it but scars and debt. */
= @5704 /* Our old leader died in battle, but his son, the one I served, he was young and bright. Full of ideas. More thinker than fighter, but that was what we needed then. We all believed he'd find us a way through it. */
= @5705 /* And he did. Adamantine. Strongest metal in the world, as far as we know. More valuable than gold. Trouble is, most of it's in the Underdark. Can't risk that. Barely enough of us left to mine iron. */
= @5706 /* Drow, though. The dark elves. They have plenty. And the ones nearer the surface, the Vhaeraunites, they'll trade when it suits them. Only problem is finding something they want. */
END
++ @5707 /* Gold? */ EXTERN GV#MOIDJ t4.14
++ @5708 /* Gems? */ EXTERN GV#MOIDJ t4.14
++ @5709 /* Weapons? */ EXTERN GV#MOIDJ t4.14
+ ~CheckStatGT(Player1,13,WIS)~ + @5710 /* People. */ EXTERN GV#MOIDJ t4.14

CHAIN GV#MOIDJ t4.14
@5710 /* People. */
= @5711 /* Human settlements, sometimes halflings or gnomes. Small ones. We'd come to them in friendship and take them in the night, and there was our adamantine. More than most clans had ever seen. Enough to start rebuilding. */
END
++ @5712 /* And you participated in this? */ EXTERN GV#MOIDJ t4.15
++ @5713 /* Smart thinking, I say. */ EXTERN GV#MOIDJ t4.16
++ @5714 /* That's absolutely despicable. */ EXTERN GV#MOIDJ t4.17

CHAIN GV#MOIDJ t4.15
@1206 /* ...yes. */
EXTERN GV#MOIDJ t4.18

CHAIN GV#MOIDJ t4.16
@5715 /* That so. Maybe you should wait for the end before you decide. */
EXTERN GV#MOIDJ t4.18

CHAIN GV#MOIDJ t4.17
@5716 /* Yes. It is. But it worked, at least for a while. */
EXTERN GV#MOIDJ t4.18

CHAIN GV#MOIDJ t4.18
@5717 /* He used convicts, you see. Long as they did the raids and kept quiet, they'd stay out of prison. But you can't just leave a pack of thieves and murderers to themselves. Someone had to keep them in line. */
= @5718 /* "It's them or us," he said. "You're my greatest warrior, my most trusted warrior. Do it for our clan. For me." */
= @5719 /* Wasn't so bad at first. I'd march the prisoners home, next day they'd be gone. Easy not to think about it. Problem is, fresh captives don't make good slaves. */
= @5720 /* You get a better deal if they're already broken in. */
END
++ @5721 /* You tortured people?! */ EXTERN GV#MOIDJ t4.19
++ @5722 /* This just keeps getting worse. */ EXTERN GV#MOIDJ t4.19
++ @5723 /* This just keeps getting better. */ EXTERN GV#MOIDJ t4.19
++ @5724 /* Ho ho! A whole different skill set! I thought you said you were only good at fighting? */ EXTERN GV#MOIDJ t4.19

CHAIN GV#MOIDJ t4.19
@5725 /* If there's one thing I can say in my defence; and it might be the only thing; it's that I wasn't involved in that part. */
= @5726 /* He had others. Professionals. I asked how he found them so quickly, where all the equipment came from, and he just gave me this...this confused little smile. Like it shouldn't even have occurred to me that he didn't already have them. */
= @5727 /* That's when I knew. This wasn't just some desperate measure. He wasn't going to stop. He'd always been this way and I never noticed. */
= @5728 /* All I ever saw was that proud face and those young, bright eyes. That's all anyone saw. Could've told the whole clan, every last detail, and not one would've believed me. He knew that. That's why he picked me. */
END
++ @5729 /* Why wouldn't they believe you specifically? */ EXTERN GV#MOIDJ t4.20
++ @5730 /* I can't believe you took part in all this. */ EXTERN GV#MOIDJ t4.21
++ @5731 /* I can respect a man like that. Putting himself and his people ahead of everyone else. */ EXTERN GV#MOIDJ t4.22
++ @5732 /* Moidre, I can't keep you in my company. Not after hearing what you've done. */ EXTERN GV#MOIDJ t4.23

CHAIN GV#MOIDJ t4.20
@5733 /* ...history. They had reason not to trust me. */
EXTERN GV#MOIDJ t4.24

CHAIN GV#MOIDJ t4.21
@5734 /* I couldn't either sometimes. But then I'd look at our clanhold and see the state we were in. The squalor. Children with bones showing through their skin. And maybe, just for the time being, we had to do what we could. */
EXTERN GV#MOIDJ t4.24

CHAIN GV#MOIDJ t4.22
@5735 /* He's dead, <CHARNAME>, and so are his people. It was all for nothing. Does that deserve respect? */
EXTERN GV#MOIDJ t4.24

CHAIN GV#MOIDJ t4.23
@5736 /* Yeah. I think...I think that might be the reason I told you. Someone like me shouldn't be around someone like you, and it's past time you learned why. */
= @5737 /* I swore to protect you, but if you won't have me, all I can do is wish you luck. I hope that's worth something to you. I hope... */
= @5738 /* Goodbye, <CHARNAME>. */
DO ~SetGlobal("GV#MOIDJoined","GLOBAL",0) LeaveParty() EscapeArea()~ EXIT

CHAIN GV#MOIDJ t4.24
@5739 /* My oath was the only thing I had, in the end. Only thing I knew. My shield is my self. And thank the gods for that. Wasn't much of myself left. */
DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTImer","GLOBAL",3600)~ EXIT


//Talk 6

CHAIN IF ~Global("GV#MOIDTalk","GLOBAL",12)~ THEN GV#MOIDJ t6
@5937 /* <CHARNAME>. */
= @8013
= @5741 /* You know the duergar? The grey dwarves? */
== GV#VYNDJ IF ~IsValidForPartyDialogue("GV#VYND")~ THEN @5742 /* Hah, here it comes. I knew it. */
END
++ @4091 /* What about them? */ EXTERN GV#MOIDJ t6.1
++ @5743 /* No, I don't. */ EXTERN GV#MOIDJ t6.2
++ @5744 /* You're one of them? */ EXTERN GV#MOIDJ t6.3


CHAIN GV#MOIDJ t6.1
@5745 /* I have their blood in me. */
EXTERN GV#MOIDJ t6.4

CHAIN GV#MOIDJ t6.2
@5746 /* From the Underdark. What drow are to elves, duergar are to dwarves. And... */
EXTERN GV#MOIDJ t6.4

CHAIN GV#MOIDJ t6.3
@5747 /* Half. */
EXTERN GV#MOIDJ t6.4

CHAIN GV#MOIDJ t6.4
@5748 /* I'm sick of pretending. You deserve to know. I'm half duergar. Half of a real dwarf. */
== GV#VYNDJ IF ~IsValidForPartyDialogue("GV#VYND")~ THEN @5749 /* ...okay, half-right. Good enough. */
== GV#MOIDJ @5750 /* My father was a duergar spy, during the war. Don't like to think what happened between him and my mother, but it happened, whatever it was. */
= @5751 /* Dunno if she knew the whole time, what she'd be bringing into the world, or if she only realised it when I came out the wrong colour. Dunno which would be worse, honestly. Either way, she decided I wasn't hers. */
= @5752 /* Remember I told you my clan leader chose me for his guard? That was why it mattered so much. Me. Half a person. Nothing to something, just like that. */
= @5753 /* Everyone advised against it. Everyone. Told him I'd turn round and break his skull the first chance I had. That's why he knew I'd keep quiet, because there was no point talking. Whatever I said against him would've meant nothing. Just hateful slander from greyskin trash. */
END
++ @5754 /* Is this why you hide your face? */ EXTERN GV#MOIDJ t6.5
++ @5755 /* So where is your father now? */ EXTERN GV#MOIDJ t6.6
++ @5756 /* Were you mistreated growing up, then? */ EXTERN GV#MOIDJ t6.7
++ @5757 /* There's no such thing as half a person. You're just a person. */ EXTERN GV#MOIDJ t6.8
++ @5758 /* Why did you tell me this? */ EXTERN GV#MOIDJ t6.9
++ @5759 /* Alright, so you're half-grey. Good to know. Was there anything else? */ EXTERN GV#MOIDJ t6.10
++ @5760 /* Is there no end to the secrets with you? If you can't be upfront with me, I think it's time you left. */ EXTERN GV#MOIDJ t6.11


APPEND GV#MOIDJ

IF ~~ t6.5
SAY @5761 /* ...I don't know. Am I hiding it? We did train to wear armour all day. It was a rare moment I bared my head, even back then. But I did. */
= @5762 /* Maybe I don't want to be seen as a duergar. Or maybe I just don't want to be seen. */
++ @5755 /* So where is your father now? */ + t6.6
++ @5756 /* Were you mistreated growing up, then? */ + t6.7
++ @5757 /* There's no such thing as half a person. You're just a person. */ + t6.8
++ @5758 /* Why did you tell me this? */ + t6.9
++ @5760 /* Is there no end to the secrets with you? If you can't be upfront with me, I think it's time you left. */ + t6.10
END

IF ~~ t6.6
SAY @5763 /* No idea. Dead, probably. Could be I killed him myself in the war. Wouldn't know him if I saw him. Wouldn't matter if I did. */
++ @5754 /* Is this why you hide your face? */ + t6.5
++ @5756 /* Were you mistreated growing up, then? */ + t6.7
++ @5757 /* There's no such thing as half a person. You're just a person. */ + t6.8
++ @5758 /* Why did you tell me this? */ + t6.9
++ @5760 /* Is there no end to the secrets with you? If you can't be upfront with me, I think it's time you left. */ + t6.10
END

IF ~~ t6.7
SAY @5764 /* The other children threw their stones, but I got used to that. It was the men and women. They didn't want me to exist, but nor they didn't want a child's blood on their hands. So they pretended I wasn't there. No matter what I did, what I said, they'd look everywhere but at me. */
= @5765 /* I hope you never know how it feels to live like that. Just a mistake that nobody wants to be the one to fix. */
++ @5754 /* Is this why you hide your face? */ + t6.5
++ @5755 /* So where is your father now? */ + t6.6
++ @5757 /* There's no such thing as half a person. You're just a person. */ + t6.8
++ @5758 /* Why did you tell me this? */ + t6.9
++ @5760 /* Is there no end to the secrets with you? If you can't be upfront with me, I think it's time you left. */ + t6.10
END

IF ~~ t6.8
SAY @5766 /* Not to my mother. Not to the clan. Not even to the duergar, if I somehow went to them. They'd cut my throat and leave me unburied. And I'd get the same from these people now, if they knew.  */
++ @5754 /* Is this why you hide your face? */ + t6.5
++ @5755 /* So where is your father now? */ + t6.6
++ @5756 /* Were you mistreated growing up, then? */ + t6.7
++ @5758 /* Why did you tell me this? */ + t6.9
++ @5760 /* Is there no end to the secrets with you? If you can't be upfront with me, I think it's time you left. */ + t6.10
END

IF ~~ t6.9
SAY @620 /* I... */
++ @5767 /* What? No answer? */ + t6.11
++ @5768 /* We both know this wasn't idle conversation. */ + t6.11
++ @5769 /* Come on, spit it out. */ + t6.11
END

IF ~~ t6.10
SAY @5770 /* ...I understand. */
= @5771 /* I don't know what my place is in the world. Maybe I was never meant to have one. I just know it's not with you. Not anymore. You deserve better than me. */
= @5772 /* I hope you find it, <CHARNAME>. */
IF ~~ DO ~SetGlobal("GV#MOIDJoined","GLOBAL",0) LeaveParty() EscapeArea()~
EXIT
END

IF ~~ t6.11
SAY @5773 /* I just needed to know. I needed to know what you'd think. What you'd say. */
++ @5774 /* Did you think it would bother me? You're a worthy companion and I trust you, no matter what's under that helmet. Stop trying to diminish yourself. */ + t6.12
++ @5775 /* I barely even know what a duergar is. I've never met one, never dealt with them. It means nothing to me. Nothing's changed. So let's just continue on. */ + t6.12
++ @5776 /* I'm not some superstitious idiot. Rumours don't interest me. Facts do. And the fact is, you fight for me and fight well. Continue to do so and we'll have no problems, whatever you are. */ + t6.12
++ @5777 /* I think you've hidden one too many things from me, and it'd be best if you went on your way before things get worse. */ + t6.10
++ @5778 /* I say go, and don't come back. I won't travel another step with you at my side. */ + t6.10
END

IF ~~ t6.12
SAY @5779 /* ...thank you. */
= @5780 /* Thank you, <CHARNAME>. */
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600)~
EXIT
END



//Talk 7
IF ~Global("GV#MOIDTalk","GLOBAL",14)~ t7
SAY @5781 /* Have you ever done something you regretted? */
++ @5782 /* Not particularly. */ + t7.1
++ @5783 /* Running from Sarevok. I should have stayed with Gorion.  */ + t7.2
++ @5784 /* More than I can remember. */ + t7.3
++ @5785 /* Drinking so much last night. */ + t7.4
END

IF ~~ t7.1
SAY @5786 /* Suppose you're still young. */
IF ~~ + t7.5
END

IF ~~ t7.2
SAY @5787 /* You'd have died. I'm not saying I don't understand. Not even saying I wouldn't have done it myself. But you would have died. */
IF ~~ + t7.5
END

IF ~~ t7.3
SAY @5788 /* You don't regret it that much if you don't remember it. */
IF ~~ + t7.5
END

IF ~~ t7.4
SAY @5789 /* No. Something real. Something that sticks with you beyond the next day. */
IF ~~ + t7.5
END

IF ~~ t7.5
SAY @5790 /* It's just...what do you do? If you can't take it back, what else is there? */
++ @5791 /* You can't undo bad deeds. You can only do good ones. */ + t7.6
++ @5792 /* Nothing. You just have to live with it. */ + t7.7
++ @5793 /* Drink to forget? */ + t7.8
++ @5794 /* Who cares? Just move on. What's done is done. */ + t7.9
END

IF ~~ t7.6
SAY @5795 /* Enough to tip the scales. Problem is, the bad ones weigh more. */
IF ~~ + t7.10
END

IF ~~ t7.7
SAY @5796 /* No. I'm done living with it. I'd rather die without it. */
IF ~~ + t7.10
END

IF ~~ t7.8
SAY @5797 /* Hah. I'm half-duergar, <CHARNAME>. There'd be droughts before I forgot. */
IF ~~ + t7.10
END

IF ~~ t7.9
SAY @5798 /* *I* care. I wouldn't have asked if I didn't care. But you don't have an answer, obviously. */
IF ~~ + t7.10
END

IF ~~ t7.10
SAY @5799 /* Maybe...maybe I could go back. To the drow camps. Some of my captives might still be alive. If I could even save *one*... */
++ @3463 /* I'd be glad to accompany you. */ + t7.11
++ @5800 /* You can't just go and storm the Underdark! That's suicide! */ + t7.12
++ @5801 /* Do you really think you could look them in the eye after what you did? */ + t7.13
++ @5802 /* Just freeing them isn't enough. You'd have to lead them to safety, help them rebuild their lives. Are you ready for that responsibility? */ + t7.14
END

IF ~~ t7.11
SAY @5803 /* No. It has to be me. Just me. I'm not dragging anyone else down. */
++ @5804 /* Going into the Underdark alone is suicide, Moidre. */ + t7.12
++ @5805 /* If that's how you want it... */ + t7.15
++ @5806 /* If you go alone, you'll die. That's all that'll happen. Do you really want to save them, or do you just want to make a noble sacrifice? */ + t7.16
END

IF ~~ t7.12
SAY @5807 /* Dying doesn't scare me. Never has. And when I meet the gods, at least I can tell them I tried. */
IF ~~ + t7.17
END

IF ~~ t7.13
SAY @5808 /* I don't have to look anyone in the eye. Not since I put this helmet on. */
IF ~~ + t7.17
END

IF ~~ t7.14
SAY @5809 /* I'm ready to try. Best anyone can say. */
IF ~~ + t7.17
END

IF ~~ t7.15
SAY @5810 /* It is. My mistake, my problem, my fight. Nobody else is suffering because of me. */
IF ~~ + t7.17
END

IF ~~ t7.16
SAY @593 /* But... */
= @5811 /* Right. You're right. If I die, I die, but it has to be for something. I need a proper plan. */
IF ~~ + t7.17
END

IF ~~ t7.17
SAY @5812 /* I doubt they're still alive, but that won't stop me. Doubt kept me in a rut for years, doing the same thing over and over again. Swearing oaths left and right just for something to hold on to. Holding a shield in front of crooks because everyone's a crook deep down, and holding a shield is all I'm good for. */
= @5813 /* I'm done listening to doubt. */
++ @5814 /* Is this where we part ways, then? */ + t7.18
++ @5815 /* About time. Do what you need to do. */ + t7.18
++ @5816 /* Just a minute. You're not done working for me yet. */ + t7.18
END

IF ~~ t7.18
SAY @5817 /* I'm not going anywhere, <CHARNAME>. Not just yet. Whatever else happens, we'll finish this first. We'll stop your enemies, because this war of theirs won't mean anything. And... */
= @5818 /* And because you deserve to live. */
IF ~~ DO ~SetGlobal("GV#MOIDFriend","GLOBAL",1) IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600)~
EXIT
END


// Talk 8
IF ~Global("GV#MOIDTalk","GLOBAL",16)~ t8
SAY @5819 /* Been doing some thinking, <CHARNAME>. About you. About this battle we're fighting. Lots of things. But mostly about myself. Took a long time, lot of wrestling and soul-searching, but I've finally come to a decision, for better or worse. */
= @5820 /* I need a bath. */
++ @5821 /* Thank the gods. */ + t8.1
++ @5822 /* Yes. You do. You do need a bath. Please take a bath, Moidre. Go and take a bath. Take one. Go. */ + t8.2
++ @5823 /* Are you inviting me? */ + t8.3
END

IF ~~ t8.1
SAY @5824 /* Yeah yeah. Got any soap? Preferably some you won't want back. */
IF ~~ + t8.4
END

IF ~~ t8.2
SAY @5825 /* Alright, alright. Don't rush me. This is big. */
IF ~~ + t8.4
END

IF ~~ t8.3
SAY @5826 /* No. If I see you, I drown you. Sod the oath. */
IF ~~ + t8.4
END

IF ~~ t8.4
SAY @5827 /* Be the first time I've disarmoured in ages, so promise you won't get hurt while I'm busy. The irony alone would kill me. */
++ @5828 /* I'm sure I can manage. */ + t8.5
++ @5829 /* I'll gladly take that risk. */ + t8.5
++ @5830 /* Anyone shows up, I'll ask if they can wait a few minutes. */ + t8.5
END

IF ~~ t8.5
SAY @5831 /* Here goes nothing... */
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",6) RestParty()~
EXIT
END


// Talk 9, after waking

IF ~Global("GV#MOIDTalk","GLOBAL",18)~ t9
SAY @5832 /* Gods. Never worked so hard in my life. */
++ @5833 /* How do you feel? */ + t9.1
++ @5834 /* No details, please. The nose knows. */ + t9.2
END

IF ~~ t9.1
SAY @5835 /* ...clean. */
++ @5836 /* That's all? */ + t9.3
++ @5837 /* And how are you finding this new sensation? */ + t9.4
++ @5838 /* Your hesitance speaks volumes. */ + t9.5
END

IF ~~ t9.2
SAY @5839 /* Glad to hear it. Let's go. */
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1)  ReallyForceSpellRES("GV#CHAR",Myself)~
EXIT
END

IF ~~ t9.3
SAY @5840 /* Refreshed. Tingly. */
IF ~~ + t9.6
END

IF ~~ t9.4
SAY @5841 /* Eh. Probably won't hold up long once we're marching again. Sweat builds up quick. Have to make it a habit, I suppose. */
IF ~~ + t9.6
END

IF ~~ t9.5
SAY @5842 /* Well what do you feel like after a bath? You've more experience than I do. Dunno why you're asking. */
IF ~~ + t9.6
END

IF ~~ t9.6
SAY @5843 /* Bit raw as well, maybe. A lot of dead skin came off. */
++ @5844 /* Any chance we could take off that helmet now? */ + t9.7
++ @5845 /* That's disgusting and you're disgusting and we're not talking about this anymore.  */ + t9.2
END

IF ~~ t9.7
SAY @5846 /* Ask me again when you're safe. */
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) ReallyForceSpellRES("GV#CHAR",Myself)~
EXIT
END





// Nashkel Mines

IF ~Global("GV#MOIDNashkelTalk","GLOBAL",1)~ t.nashkel
SAY @5847 /* That's your end of the bargain, then. I'm here till you say otherwise. */
++ @5848 /* What, just like that? */ + t.nashkel1
++ @5849 /* I plan to track down these bandits, so you'd better be sure. */ + t.nashkel2
++ @5850 /* Glad to have you. Just be warned, it's not going to get less dangerous. */ + t.nashkel2
++ @5851 /* I'm saying otherwise now. Time you went on your way. */ + t.nashkel3
++ @5852 /* Good. Don't speak until I say otherwise either. */ DO ~SetGlobal("GV#MOIDNashkelTalk","GLOBAL",2) SetGlobal("GV#MOIDShutUp","GLOBAL",1)~ EXIT
END

IF ~~ t.nashkel1
SAY @5853 /* That was the deal. */
IF ~~ + t.nashkel4
END

IF ~~ t.nashkel2
SAY @5854 /* Makes no difference if you're invading the Nine Hells. I gave the oath. */
IF ~~ + t.nashkel4
END

IF ~~ t.nashkel3
SAY @5855 /* You're the boss. */
IF ~~ DO ~SetGlobal("GV#MOIDNashkelTalk","GLOBAL",2) SetGlobal("GV#MOIDJoined","GLOBAL",1) LeaveParty() EscapeArea()~ EXIT
END

IF ~~ t.nashkel4
SAY @5856 /* Not like I have much else anyway. Least you did something worthwhile. More than I can say for most clients. */
++ @5857 /* If I can do something to help people, I will. */ + t.nashkel5
++ @5858 /* Don't start thinking this is some charity mission. I'm doing this for my own sake.  */ + t.nashkel6
++ @5859 /* You'll find there's much more you can say for me than most people. */ + t.nashkel7
++ @5860 /* And who were these other clients? */ + t.nashkel8
END

IF ~~ t.nashkel5
SAY @5861 /* Easily said. */
IF ~~ DO ~SetGlobal("GV#MOIDNashkelTalk","GLOBAL",2)~ EXIT
END

IF ~~ t.nashkel6
SAY @5862 /* Everyone's got an agenda. No need to warn me. */
IF ~~ DO ~SetGlobal("GV#MOIDNashkelTalk","GLOBAL",2)~ EXIT
END

IF ~~ t.nashkel7
SAY @5863 /* Not necessarily a good thing. */
IF ~~ DO ~SetGlobal("GV#MOIDNashkelTalk","GLOBAL",2)~ EXIT
END

IF ~~ t.nashkel8
SAY @5864 /* No-one you'd know, and no-one you'd need to. */
IF ~~ DO ~SetGlobal("GV#MOIDNashkelTalk","GLOBAL",2)~ EXIT
END



// Cloakwood Mines


IF ~Global("GV#MOIDCloakwoodTalk","GLOBAL",1)~ t.cloakwood
SAY @5865 /* *sigh* Knew there'd be slaves. Knew it. */
++ @5866 /* We'll free them. I promise. */ + t.cloakwood1
++ @5867 /* Of course there are. Nobody *wants* to do back-breaking labour for evil organisations. */ + t.cloakwood2
++ @5868 /* Does that bother you? */ + t.cloakwood3
++ @5869 /* Anyone too weak to seize their freedom deserves slavery. */ + t.cloakwood4
END

IF ~~ t.cloakwood1
SAY @5870 /* Don't. Don't make promises. If you really want to do something, you shouldn't need to promise. Just do it. */
IF ~~ + t.cloakwood5
END

IF ~~ t.cloakwood2
SAY @5871 /* Nobody wants to do back-breaking labour at all. It's just better than starving. */
IF ~~ + t.cloakwood5
END

IF ~~ t.cloakwood3
SAY @5872 /* I don't understand why you need to ask that. */
IF ~~ + t.cloakwood5
END

IF ~~ t.cloakwood4
SAY @5873 /* That so? Alright. Let's have you stripped and beaten until you can't stand up. Let's chain you to a wall so long you forget how your arms work. Let's leave you starving alone in the darkness until your brain rots. Then you can look me in the eye and tell me you don't deserve what you can't win back. */
= @5874 /* There's no fair fight, you ignorant bastard. You don't have your swords and your magic, or your friends. You don't have anything but the work you're given. That's your world, and it's too small for freedom. */
IF ~~ DO ~SetGlobal("GV#MOIDCloakwoodTalk","GLOBAL",2)~ EXIT
END

IF ~~ t.cloakwood5
SAY @5875 /* Free men. That's all I wanted to see in this place. Desperate fools who took any job they could get. But there's no sense buying what you can take, is there? No sense. */
IF ~~ DO ~SetGlobal("GV#MOIDCloakwoodTalk","GLOBAL",2)~ EXIT
END

// Flooded Mines, Slaves Freed

IF ~Global("GV#MOIDFloodedTalk","GLOBAL",1)~ t.flood
SAY @5876 /* I'm glad you did that. Helping those slaves, drowning their prison. */
++ @5877 /* Anyone would have. */  + t.flood1
++ @5878 /* Finally coming around on me, are you? */ + t.flood2
++ @5879 /* I didn't do it for your praise. */ + t.flood3
END

IF ~~ t.flood1
SAY @5880 /* If you really think that's true, I feel sorry for you. And I envy you. But you're probably just saying it. */
IF ~~ + t.flood4
END

IF ~~ t.flood2
SAY @5881 /* Maybe. Don't know your reasons, but it hardly matters. Result's the same. */
IF ~~ + t.flood4
END

IF ~~ t.flood3
SAY @5882 /* Then I'm even more glad. */
IF ~~ + t.flood4
END

IF ~~ t.flood4
SAY @5883 /* Lost more than their freedom, though. Too bad you can't give the rest back. */
++ @5884 /* Perhaps not. But we've given them the chance to find it again. */  + t.flood5
++ @5885 /* I can't fix every one of every person's problems, Moidre. */ + t.flood6
++ @5886 /* I didn't expect you to care so much. */ + t.flood7
END

IF ~~ t.flood5
SAY @5887 /* Not we. You. You did that. I'm just here. */
IF ~~ DO ~SetGlobal("GV#MOIDFloodedTalk","GLOBAL",2) RealSetGlobalTimer("GV#MOIDRepTimer","GLOBAL",3600)~ EXIT
END

IF ~~ t.flood6
SAY @5888 /* Don't expect you to. Just saying. */
IF ~~ DO ~SetGlobal("GV#MOIDFloodedTalk","GLOBAL",2) RealSetGlobalTimer("GV#MOIDRepTimer","GLOBAL",3600)~ EXIT
END

IF ~~ t.flood7
SAY @5889 /* Not sure I did either. */
IF ~~ DO ~SetGlobal("GV#MOIDFloodedTalk","GLOBAL",2) RealSetGlobalTimer("GV#MOIDRepTimer","GLOBAL",3600)~ EXIT
END


// Flooded Mines, Slaves Drowned

IF ~Global("GV#MOIDDrownedTalk","GLOBAL",1)~ t.drown
SAY @324 /* Why? */
++ @5890 /* Why what? */ + t.drown1
++ @5891 /* Because. */ + t.drown1
++ @5892 /* Be quiet. */ + t.drown1
END

IF ~~ t.drown1
SAY @5893 /* Why drown those people? What did they do to you? */
++ @5894 /* Those mines had to go, and I wasn't going to spend my hard-earned gold just to spare a few slaves. */ + t.drown2
++ @5895 /* It sounded like fun. And it was! Didn't you see that man's face when he realised we lied? */ + t.drown3
++ @5896 /* Does it matter? */ + t.drown4
++ @5897 /* Why didn't you stop me? */ + t.drown5
END

IF ~~ t.drown2
SAY @5898 /* You could have warned them. Give them some kind of chance. What would that have cost you? */
++ @5899 /* Time and patience. Those people meant nothing to me, nor should they to you. */ + t.drown3
++ @5900 /* The amusement of seeing them all die, obviously. */ + t.drown3
++ @5896 /* Does it matter? */ + t.drown4
++ @5901 /* If you found it so objectionable, why didn't you stop me? */ + t.drown5
END

IF ~~ t.drown3
SAY @5902 /* I've never known a monster so honest. */
++ @5903 /* That's right, and you never will. I know what I am, and I have the spine to admit it. */ + t.drown6
++ @5904 /* What are you trying to tell me here? */ + t.drown6
++ @5905 /* I'm a pragmatist, Moidre. You know damn well there's worse than me out there. */ + t.drown6
++ @5901 /* If you found it so objectionable, why didn't you stop me? */ + t.drown5
++ @5906 /* If you don't like it, go. */ + t.drown7
END

IF ~~ t.drown4
SAY @640 /* ...no. */
= @5907 /* It doesn't matter. However you justify it, it won't give those people their lives back. */
IF ~~ + t.drown6
END

IF ~~ t.drown5
SAY @5908 /* Would you have listened? */
++ @381 /* No. */ + t.drown8
++ @5909 /* Depends on what you said. */ + t.drown8
++ @5910 /* I never asked why you didn't convince me. Why didn't you *stop* me? */ + t.drown9
END

IF ~~ t.drown6
SAY @5911 /* I'll do what I swore to do, <CHARNAME>. Anyone tries to kill you, I'll fight them. And that's it. Never ask me for anything else. */
IF ~~ DO ~SetGlobal("GV#MOIDUnhappy","GLOBAL",1) SetGlobal("GV#MOIDShutUp","GLOBAL",1) ReallyForceSpellRES("GV#STRE",Myself)~ EXIT
END

IF ~~ t.drown7
SAY @5912 /* I have permission? */
++ @1376 /* Yes. */ + t.drown10
++ @5913 /* Of course you don't. You thought I was serious? Get back in line. */ + t.drown6
END

IF ~~ t.drown8
SAY @5914 /* No. You wouldn't have. If you have it in you to do something like that, nothing I said would've made a difference. */
IF ~~ + t.drown11
END

IF ~~ t.drown9
SAY @5915 /* I...I made an oath. Guards who turn on their ward are less than worthless.  */
IF ~~ + t.drown11
END

IF ~~ t.drown10
SAY @5916 /* You're releasing me from the oath. I don't have to protect you anymore. */
++ @2727 /* That's right. */ + t.drown12
++ @5917 /* Of course I'm not. You thought I was serious? Get back in line. */ + t.drown6
END

IF ~~ t.drown11
SAY @5918 /* Don't try to make this my fault. You did it. Even if I should have stopped you, YOU DID IT. */
++ @5919 /* And you're an accomplice. Deal with it. */ + t.drown6
++ @5920 /* Whenever you're done whining, let me know. */ + t.drown6
++ @5921 /* If it bothers you so much, you can go. */ + t.drown7
END

IF ~~ t.drown12
SAY @2256 /* Good. */
IF ~~ DO ~SetGlobal("GV#MOIDJoined","GLOBAL",0) LeaveParty() Enemy() Attack(Player1)~ EXIT
END




// High rep after flooding mines

IF ~Global("GV#MOIDRepTalk","GLOBAL",1)~ t.rep
SAY @5922 /* You're making a fair name for yourself. */
++ @5923 /* I'm just trying to do what's right. */ + t.rep1
++ @5924 /* I couldn't have done it without you. */ + t.rep2
++ @5925 /* Do you approve? */ + t.rep3
++ @5926 /* It's always better to make friends than enemies. */ + t.rep4
END

IF ~~ t.rep1
SAY @5927 /* Not just trying. Succeeding. */
IF ~~ + t.rep5
END

IF ~~ t.rep2
SAY @5928 /* Yeah you could've. See it clearer every day. You don't need me. */
IF ~~ + t.rep5
END

IF ~~ t.rep3
SAY @5929 /* Getting hard to say I don't. */
IF ~~ + t.rep5
END

IF ~~ t.rep4
SAY @5930 /* But you are making enemies. Can't do the right thing these days without stepping on someone's toes. */
IF ~~ + t.rep5
END

IF ~~ t.rep5
SAY @5931 /* I thought you just wanted the fame at first. Free drinks and a hero's discount. Been waiting to see what's hiding beneath it all. */
= @5932 /* But I keep coming back to what you did for those slaves. Paid out of your own pocket, risked drowning, and I can't figure out what you stood to gain. */
++ @5933 /* Why are you always looking for a hidden agenda? */ + t.rep6
++ @5934 /* Being able to live with myself is a fair gain. */ + t.rep7
++ @5935 /* Gold is heavy. And I always enjoy a good swim. */ + t.rep7
END

IF ~~ t.rep6
SAY @5936 /* Spotting danger's part of the job. Nothing's more dangerous than ulterior motives. */
IF ~~ + t.rep7
END

IF ~~ t.rep7
SAY @5937 /* <CHARNAME>. */
= @5938 /* Can I trust you? */
++ @5939 /* Of course you can. */ + t.rep8
++ @5940 /* Trust me to do what? */ + t.rep9
++ @5941 /* Nope! I break promises like eggs. */ + t.rep10
END

IF ~~ t.rep8
SAY @5942 /* Don't answer like it's a small question. */
IF ~~ + t.rep11
END

IF ~~ t.rep9
SAY @5943 /* To be what you seem to be. */
IF ~~ + t.rep11
END

IF ~~ t.rep10
SAY @5944 /* Don't be flippant. Look at me.  */
IF ~~ + t.rep11
END

IF ~~ t.rep11
SAY @5945 /* Words are just words. I know that. But if you say it, really say it, at least I have something to hold you to. */
= @5938 /* Can I trust you? */
++ @5946 /* I won't disappoint you. You have my word. */ + t.rep12
++ @5947 /* I don't know if I can make that kind of promise. */ + t.rep13
++ @5948 /* No. You can't. */ + t.rep13
END

IF ~~ t.rep12
SAY @5949 /* ...alright. Can't ask much more than that. */
IF ~~ + t.rep14
END

IF ~~ t.rep13
SAY @5950 /* Then you're honest. Still more than I'd hoped. */
IF ~~ + t.rep14
END

IF ~~ t.rep14
SAY @5951 /* I want to believe I'm protecting something good. Just for a little while. Please don't let me down. */
IF ~~ DO ~SetGlobal("GV#MOIDRepTalk","GLOBAL",2) SetGlobal("GV#MOIDHappy","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600)~ EXIT
END

// Reputation drop after flooding mines
IF ~Global("GV#MOIDRepDropTalk","GLOBAL",1)~ t.drop
SAY @5952 /* Here it is. Finally. The face behind the mask. */
= @5953 /* Had me fooled, <CHARNAME>. You really did. Swore I'd never be fooled again, and you went and made me betray myself. Of all the lying bastards I've worked for, you lie the best. */
++ @5954 /* It's just a setback, Moidre. I'll be back to heroics in no time. */ + t.drop1
++ @5181 /* It's fine. Just as long as there aren't any other long-lost relatives lurking out there. */ + t.drop2
++ @5182 /* How are you feeling about it now? */ + t.drop3
END

IF ~~ t.drop1
SAY @5955 /* Yeah. Only temporary, right? Just for now. Just until you get through this. We all have to bend sometimes. */
= @5956 /* Heard it all before. Once was enough. */
IF ~~ + t.drop4
END

IF ~~ t.drop2
SAY @5957 /* Call it what you like. Doesn't matter. Nothing you say matters anymore. */
IF ~~ + t.drop4
END

IF ~~ t.drop3
SAY @5958 /* Nothing. I think you know that. Think that's why you dropped the facade. I'm just a fighter. That's all I am, all I'll ever be. And I can't fight you because I swore I wouldn't. */
IF ~~ + t.drop4
END

IF ~~ t.drop4
SAY @5959 /* I'll uphold my oath. It's all I can do. And from now on, it's all I will do. Don't expect anything else. */
IF ~~ DO ~SetGlobal("GV#MOIDRepDropTalk","GLOBAL",2) SetGlobal("GV#MOIDHappy","GLOBAL",0) SetGlobal("GV#MOIDUnhappy","GLOBAL",1) SetGlobal("GV#MOIDShutUp","GLOBAL",1) ReallyForceSpellRES("GV#STRE",Myself)~ EXIT
END



//Approaching Sarevok
IF ~Global("GV#MOIDSarevokTalk","GLOBAL",1) Global("GV#MOIDFriend","GLOBAL",1)~ t.sarevok
SAY @5960 /* <CHARNAME>, before we go in there, I need a word. */
++ @5961 /* What about? */ + t.sarevok1
++ @5962 /* Not now. Let's get this done. */ DO ~SetGlobal("GV#MOIDSarevokTalk","GLOBAL",4)~ EXIT
END

IF ~~ t.sarevok1
SAY @5963 /* Come aside. Won't take long. */
= @5964 /* There are only two outcomes here. Either I help you win this fight, or I die trying. I'm ready for either one. */
= @5965 /* If I do fall, I don't expect you to remember me. Not even sure I want you to. Might be for the best if my time in this world was just forgotten. But you'll remember anyway, I think, whether I want it or not. Whether I expect it or not. So... */
IF ~~ DO ~ClearAllActions() StartCutSceneMode() StartCutScene("GV#CMOID")~ EXIT
END

IF ~Global("GV#MOIDSarevokTalk","GLOBAL",2) Global("GV#MOIDFriend","GLOBAL",1)~ t.sarevok2
SAY @5966 /* Remember this. Let the memory be a face, not just a shadow behind a visor. */
++ @5967 /* I remember people by their deeds and their character, Moidre. You'll be more than just a face. */ + t.sarevok3
++ @5968 /* Come now, no talk of what will happen if we die. This isn't the day. */ + t.sarevok4
++ @5969 /* Agh, my memory for faces is terrible. It's so much easier thinking of you as a walking suit of armour. */ + t.sarevok5
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",18)~ + @5970 /* First a bath, now the helmet? I half expect to see you singing and dancing at this rate. */ + t.sarevok6
END

IF ~~ t.sarevok3
SAY @5971 /* If that's true, I hope I live. I'm not proud of my character or my deeds, <CHARNAME>, but if I have enough time...maybe someday, I can be. */
IF ~~ + t.sarevok7
END

IF ~~ t.sarevok4
SAY @5972 /* If I have anything to say about it, no. It isn't. */
IF ~~ + t.sarevok7
END

IF ~~ t.sarevok5
SAY @5973 /* That's fair. Most of my life, that's all I've been. */
IF ~~ + t.sarevok7
END

IF ~~ t.sarevok6
SAY @5974 /* Not in your lifetime. I'll swear another oath on it. */
IF ~~ + t.sarevok7
END

IF ~~ t.sarevok7
SAY @5975 /* How do I...how do I look? */
++ @5976 /* Beautiful, in your own way. Although your hair's a disaster. */ + t.sarevok8
++ @5977 /* Normal. Just a normal woman. It's a bit disappointing after building up all that mystery. */ + t.sarevok8
++ @5978 /* Gods, no wonder you wear that thing all day. You're hideous. */ + t.sarevok8
++ @5979 /* I never thought I'd hear that question from you, of all people. */ + t.sarevok9
END

IF ~~ t.sarevok8
SAY @5980 /* I'll have to take your word. Don't even remember myself anymore. All I'd see in a mirror is a stranger. */
IF ~~ + t.sarevok10
END

IF ~~ t.sarevok9
SAY @5981 /* Just curiosity. It's been so long, I'm not even sure I'd recognise myself anymore. Thought I might as well give myself some idea. */
IF ~~ + t.sarevok10
END

IF ~~ t.sarevok10
SAY @5982 /* Doesn't matter. */
IF ~~ DO ~ClearAllActions() StartCutSceneMode() StartCutScene("GV#CMOI1")~ EXIT
END

IF ~Global("GV#MOIDSarevokTalk","GLOBAL",3) Global("GV#MOIDFriend","GLOBAL",1)~ t.sarevok11
SAY @5983 /* Won't kill Sarevok with my looks. */
IF ~~ DO ~SetGlobal("GV#MOIDSarevokTalk","GLOBAL",4)~ EXIT
END





//PIDs


IF ~IsGabber(Player1) GlobalGT("GV#MOIDTalk","GLOBAL",2) Global("GV#MOIDUnhappy","GLOBAL",0)~ GV#MOIDPID
SAY @5984 /* Hm? */
++ @3586 /* How are you? */ + p1.1
+ ~Global("GV#MOIDFriend","GLOBAL",0)~ + @3587 /* How do you think we're doing so far? */ + p2.1
+ ~Global("GV#MOIDUnhappy","GLOBAL",0) Global("GV#MOIDFriend","GLOBAL",1)~ + @3587 /* How do you think we're doing so far? */ + p2.2
+ ~Global("GV#MOIDPidIron","GLOBAL",0) GlobalLT("CHAPTER","GLOBAL",%tutu_chapter_3%)~ + @5985 /* Do you have any ideas as to what's affecting the local iron? */ DO ~SetGlobal("GV#MOIDPidIron","GLOBAL",1)~ + p.iron
+ ~Global("GV#MOIDPidBathe","GLOBAL",0) GlobalLT("GV#MOIDTalk","GLOBAL",18)~ + @5986 /* Could I impose on you to bathe? */ DO ~SetGlobal("GV#MOIDPidBathe","GLOBAL",1)~ + p.bathe
+ ~Global("GV#MOIDPidHelmet","GLOBAL",0)~ + @5987 /* Don't you ever take that helmet off? */ DO ~SetGlobal("GV#MOIDPidHelmet","GLOBAL",1)~ + p.helmet
+ ~!Race(Player1,DWARF) Global("GV#MOIDPidBeard","GLOBAL",0)~ + @5988 /* I can't help noticing you have a beard. */ DO ~SetGlobal("GV#MOIDPidBeard","GLOBAL",1)~ + p.beard
+ ~Race(Player1,DWARF) Global("GV#MOIDPidBeard","GLOBAL",0)~ + @5989 /* It's refreshing to see a dwarven woman wear her beard so proudly. */ DO ~SetGlobal("GV#MOIDPidBeard","GLOBAL",1)~ + p.beard2
+ ~Global("GV#MOIDPidMines","GLOBAL",0) GlobalLT("CHAPTER","GLOBAL",%tutu_chapter_3%)~ + @5990 /* Why not investigate the mines yourself? You're a strong fighter; you could easily round up a group and take care of it. */ DO ~SetGlobal("GV#MOIDPidMines","GLOBAL",1)~ + p.mines
+ ~Global("GV#MOIDPidMines","GLOBAL",0) GlobalGT("CHAPTER","GLOBAL",%tutu_chapter_2%)~ + @5991 /* Why didn't you investigate the mines yourself? You're a strong fighter; you could easily have rounded up a group and taken care of it. */ DO ~SetGlobal("GV#MOIDPidMines","GLOBAL",1)~ + p.mines
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",4) Global("GV#MOIDPidSuggest","GLOBAL",0)~ + @5992 /* Any suggestions on what to do next? */ DO ~SetGlobal("GV#MOIDPidSuggest","GLOBAL",1)~ + p.suggest
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",4) Global("GV#MOIDPidGold","GLOBAL",0)~ + @5993 /* You didn't have much gold when I met you. Has business been slow? */ DO ~SetGlobal("GV#MOIDPidGold","GLOBAL",1)~ + p.gold
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",4) Global("GV#MOIDPidJobs","GLOBAL",0)~ + @5994 /* I'd like to hear more about these past jobs of yours. */ DO ~SetGlobal("GV#MOIDPidJobs","GLOBAL",1)~ + p.jobs
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",6) Global("GV#MOIDPidWhere","GLOBAL",0)~ + @5995 /* Where are you from, Moidre? */ DO ~SetGlobal("GV#MOIDPidWhere","GLOBAL",1)~ + p.where
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",6) Global("GV#MOIDPidSullen","GLOBAL",0)~ + @5996 /* Were you born sullen or did you grow into it? */ DO ~SetGlobal("GV#MOIDPidSullen","GLOBAL",1)~ + p.sullen
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",6) Global("GV#MOIDPidHair","GLOBAL",0)~ + @5997 /* What do you do when you need to cut your hair? */ DO ~SetGlobal("GV#MOIDPidHair","GLOBAL",1)~ + p.hair
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",8) Global("GV#MOIDPidTravel","GLOBAL",0)~ + @5998 /* I don't understand why you're still travelling with me. */ DO ~SetGlobal("GV#MOIDPidTravel","GLOBAL",1)~ + p.travel
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",8) Global("GV#MOIDPidHigh","GLOBAL",0)~ + @5999 /* You don't seem to think very highly of yourself. */ DO ~SetGlobal("GV#MOIDPidHigh","GLOBAL",1)~ + p.high
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",8) Global("GV#MOIDPidWork","GLOBAL",0)~ + @6000 /* What do you do when you're not working? */ DO ~SetGlobal("GV#MOIDPidWork","GLOBAL",1)~ + p.work
+ ~Global("GV#MOIDCloakwoodTalk","GLOBAL",2) Global("GV#MOIDPidSlave","GLOBAL",0)~ + @6001 /* Were you a slave? */ DO ~SetGlobal("GV#MOIDPidSlave","GLOBAL",1)~ + p.slave
+ ~Global("GV#MOIDHappy","GLOBAL",1) GlobalGT("CHAPTER","GLOBAL",%tutu_chapter_6%) Global("GV#MOIDPidFist","GLOBAL",0) !Dead("Rieltar")~ + @6002 /* Don't you object to fighting the Flaming Fist for me? */ DO ~SetGlobal("GV#MOIDPidFist","GLOBAL",1)~ + p.fist
+ ~Global("GV#MOIDHappy","GLOBAL",1) GlobalGT("CHAPTER","GLOBAL",%tutu_chapter_6%) Global("GV#MOIDPidFist","GLOBAL",0) Dead("Rieltar")~ + @6002 /* Don't you object to fighting the Flaming Fist for me? */ DO ~SetGlobal("GV#MOIDPidFist","GLOBAL",1)~ + p.fist1
+ ~Global("GV#MOIDHappy","GLOBAL",1) Global("GV#MOIDPidBounce","GLOBAL",0)~ + @6003 /* You've a bit more bounce in your step lately. */ DO ~SetGlobal("GV#MOIDPidBounce","GLOBAL",1)~ + p.bounce
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",10) Global("GV#MOIDPidClan","GLOBAL",0)~ + @6004 /* What happened to your clan, in the end? */ DO ~SetGlobal("GV#MOIDPidClan","GLOBAL",1)~ + p.clan
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",12) Global("GV#MOIDPidDuergar","GLOBAL",0)~ + @6005 /* So what sets a half-duergar apart from a full one? */ DO ~SetGlobal("GV#MOIDPidDuergar","GLOBAL",1)~ + p.duergar
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",12) Global("GV#MOIDPidLife","GLOBAL",0)~ + @6006 /* If you could do absolutely anything else with your life, what would it be? */ DO ~SetGlobal("GV#MOIDPidLife","GLOBAL",1)~ + p.life
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",14) Global("GV#MOIDPidRecognise","GLOBAL",0)~ + @6007 /* If you do manage to find those slaves, do you think they'll recognise you? */ DO ~SetGlobal("GV#MOIDPidRecognise","GLOBAL",1)~ + p.recognise
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",18) Global("GV#MOIDPidSoap","GLOBAL",0)~ + @6008 /* Can I get my soap back now? */ DO ~SetGlobal("GV#MOIDPidSoap","GLOBAL",1)~ + p.soap
+ ~Global("GV#MOIDHappy","GLOBAL",1) Global("GV#MOIDPidBhaal","GLOBAL",0) PartyHasItem("%tutu_var%SCRL2J")~ + @6009 /* Moidre, Gorion's letter said I'm part of Alaundo's prophecy. I'm one of Bhaal's children. */ DO ~SetGlobal("GV#MOIDPidBhaal","GLOBAL",1)~ + p.bhaal
+ ~Global("GV#MOIDPidAjantis","GLOBAL",0) Global("GV#MOIDAjantis2","GLOBAL",1) InParty("Ajantis")~ + @6010 /* Those shield lessons you're giving Ajantis. Is that really what dwarves are taught? */ DO ~SetGlobal("GV#MOIDPidAjantis","GLOBAL",1)~ + p.ajantis
+ ~Global("GV#MOIDPidAlora","GLOBAL",0) Global("GV#MOIDAlora3","GLOBAL",1) InParty("Alora")~ + @6011 /* I can't believe Alora actually got you to smile. */ DO ~SetGlobal("GV#MOIDPidAlora","GLOBAL",1)~ + p.alora
+ ~Global("GV#MOIDPidImoen","GLOBAL",0) Global("GV#MOIDImoen2","GLOBAL",1) InParty("Imoen")~ + @6012 /* Would you really fight your own friends and family for me? */ DO ~SetGlobal("GV#MOIDPidImoen","GLOBAL",1)~ + p.imoen
+ ~Global("GV#MOIDPidJaheira","GLOBAL",0) Global("GV#MOIDJaheira2","GLOBAL",1) InParty("Jaheira")~ + @6013 /* That poison rain thing didn't actually happen, did it? */ DO ~SetGlobal("GV#MOIDPidJaheira","GLOBAL",1)~ + p.jaheira
+ ~Global("GV#MOIDPidMontaron","GLOBAL",0) Global("GV#MOIDMontaron1","GLOBAL",1) InParty("Montaron")~ + @6014 /* I take it you don't trust Montaron. */ DO ~SetGlobal("GV#MOIDPidMontaron","GLOBAL",1)~ + p.montaron
+ ~Global("GV#MOIDPidMinsc","GLOBAL",0) Global("GV#MOIDMinsc2","GLOBAL",1) InParty("Minsc")~ + @6015 /* Don't make Minsc think too hard. It hurts to watch. */  DO ~SetGlobal("GV#MOIDPidMinsc","GLOBAL",1)~+ p.minsc
+ ~Global("GV#MOIDPidNeera","GLOBAL",0) Global("GV#MOIDNeera1","GLOBAL",1) InParty("Neera")~ + @6016 /* You didn't need to threaten Neera like that. */  DO ~SetGlobal("GV#MOIDPidNeera","GLOBAL",1)~+ p.neera
+ ~Global("GV#MOIDPidRasaad","GLOBAL",0) Global("GV#MOIDRasaad2","GLOBAL",1) InParty("Rasaad")~ + @6017 /* It sounds like Rasaad's making a believer out of you. */ DO ~SetGlobal("GV#MOIDPidRasaad","GLOBAL",1)~ + p.rasaad
+ ~Global("GV#MOIDPidSafana","GLOBAL",0) Global("GV#MOIDSafana2","GLOBAL",1) InParty("Safana")~ + @6018 /* That perfume is intoxicating on you. */ DO ~SetGlobal("GV#MOIDPidSafana","GLOBAL",1)~ + p.safana
+ ~Global("GV#MOIDPidSharteel","GLOBAL",0) Global("GV#MOIDSharteel2","GLOBAL",1) InParty("Sharteel")~ + @6019 /* Why so sensitive about the beard? */ DO ~SetGlobal("GV#MOIDPidSharteel","GLOBAL",1)~ + p.sharteel
+ ~Global("GV#MOIDPidSkie","GLOBAL",0) Global("GV#MOIDSkie3","GLOBAL",1) InParty("Skie")~ + @6020 /* I think Skie's a lost cause. */ DO ~SetGlobal("GV#MOIDPidSkie","GLOBAL",1)~ + p.skie
+ ~Global("GV#MOIDPidViconia","GLOBAL",0) Global("GV#MOIDViconia1","GLOBAL",1) InParty("Viconia")~ + @6021 /* Why do you care about Viconia? */ DO ~SetGlobal("GV#MOIDPidViconia","GLOBAL",1)~ + p.viconia
+ ~Global("GV#MOIDPidXan","GLOBAL",0) Global("GV#MOIDXan1","GLOBAL",1) InParty("Xan")~ + @6022 /* So much for cheering Xan up, eh? */ DO ~SetGlobal("GV#MOIDPidXan","GLOBAL",1)~ + p.xan
+ ~Global("GV#MOIDPidYeslick","GLOBAL",0) Global("GV#MOIDYeslick3","GLOBAL",1) InParty("Yeslick")~ + @6023 /* If @117 /* Moidre */ is just your clan name, what's your actual name? */ DO ~SetGlobal("GV#MOIDPidYeslick","GLOBAL",1)~ + p.yeslick
+ ~Global("GV#MOIDPidGirdle","GLOBAL",0) Gender("GV#MOID",MALE)~ + @3622 /* How's that girdle treating you? */ DO ~SetGlobal("GV#MOIDPidGirdle","GLOBAL",1)~ + p.girdle
+ ~Global("GV#MOIDPidDrink","GLOBAL",0) Global("GV#MOIDHappy","GLOBAL",0) OR(21)
        AreaCheck("%BaldursGateDocks_JopalinsTavern%")
     	AreaCheck("%Beregost_FeldepostsInn_L1%")
     	AreaCheck("%Beregost_JovialJuggler_L1%")
     	AreaCheck("%Beregost_RedSheaf_L1%")
     	AreaCheck("%Beregost_BurningWizard_L1%")
     	AreaCheck("%EBaldursGate_KeexieTavern_L1%")
     	AreaCheck("%EBaldursGate_ElfsongTavern_L1%")
     	AreaCheck("%EBaldursGate_Inn_L1%")
     	AreaCheck("%FriendlyArmInn_L1%")
     	AreaCheck("%Nashkel_Inn%")
     	AreaCheck("%NBaldursGate_ThreeOldKegs_L1%")
     	AreaCheck("%NEBaldursGate_BlushingMermaid_L1%")
     	AreaCheck("%NEBaldursGate_SplurgingSturgeon_L2%")
     	AreaCheck("%NEBaldursGate_SplurgingSturgeon_L1%")
     	AreaCheck("%NWBaldursGate_Helm&Cloak_L1%")
     	AreaCheck("%NWBaldursGate_Tavern_L1%")
     	AreaCheck("%SEBaldursGate_Inn_L1%")
     	AreaCheck("%SEBaldursGate_Blade&Stars_L1%")
     	AreaCheck("%SWBaldursGate_Tavern_L1%")
     	AreaCheck("%SWBaldursGate_YeOldeInn_L1%")
     	AreaCheck("%UlgothsBeard_Inn%")
     	~
        + @6024 /* Want a drink, Moidre? */ DO ~SetGlobal("GV#MOIDPidDrink","GLOBAL",1)~ + p.drink

+ ~Global("GV#MOIDPidDrink","GLOBAL",0) Global("GV#MOIDHappy","GLOBAL",1) OR(21)
        AreaCheck("%BaldursGateDocks_JopalinsTavern%")
     	AreaCheck("%Beregost_FeldepostsInn_L1%")
     	AreaCheck("%Beregost_JovialJuggler_L1%")
     	AreaCheck("%Beregost_RedSheaf_L1%")
     	AreaCheck("%Beregost_BurningWizard_L1%")
     	AreaCheck("%EBaldursGate_KeexieTavern_L1%")
     	AreaCheck("%EBaldursGate_ElfsongTavern_L1%")
     	AreaCheck("%EBaldursGate_Inn_L1%")
     	AreaCheck("%FriendlyArmInn_L1%")
     	AreaCheck("%Nashkel_Inn%")
     	AreaCheck("%NBaldursGate_ThreeOldKegs_L1%")
     	AreaCheck("%NEBaldursGate_BlushingMermaid_L1%")
     	AreaCheck("%NEBaldursGate_SplurgingSturgeon_L2%")
     	AreaCheck("%NEBaldursGate_SplurgingSturgeon_L1%")
     	AreaCheck("%NWBaldursGate_Helm&Cloak_L1%")
     	AreaCheck("%NWBaldursGate_Tavern_L1%")
     	AreaCheck("%SEBaldursGate_Inn_L1%")
     	AreaCheck("%SEBaldursGate_Blade&Stars_L1%")
     	AreaCheck("%SWBaldursGate_Tavern_L1%")
     	AreaCheck("%SWBaldursGate_YeOldeInn_L1%")
     	AreaCheck("%UlgothsBeard_Inn%")
     	~
        + @6024 /* Want a drink, Moidre? */ DO ~SetGlobal("GV#MOIDPidDrink","GLOBAL",1)~ + p.drink2
++ @3624 /* Never mind. */ EXIT
END

IF ~~ p1.1
SAY @6025 /* Fine. */
IF ~~ EXIT
END

IF ~~ p2.1
SAY @6026 /* You're not dead yet. That's my metric. Doing fine. */
IF ~~ EXIT
END

IF ~~ p2.2
SAY @6027 /* Just keep doing what you're doing, <CHARNAME>. I've got your back. */
IF ~~ EXIT
END


IF ~~ p.iron
SAY @6028 /* You asking because I'm a dwarf? */
++ @6029 /* I wasn't going to say it, but... */ + p.iron1
++ @6030 /* It just seemed like you knew something about it. */ + p.iron2
END

IF ~~ p.iron1
SAY @6031 /* You're not the first. Live underground, everyone takes you for a mining expert. */
IF ~~ + p.iron2
END

IF ~~ p.iron2
SAY @6032 /* It's sabotage. Only answer that makes sense. Someone looking to invade or monopolise. Couldn't say who, though. */
++ @6033 /* You think Amn might be involved? */ + p.iron3
++ @6034 /* Is there some way to counteract it? */ + p.iron4
++ @6035 /* What makes you say that? Maybe the iron in that mine is just going bad. */ + p.iron5
END

IF ~~ p.iron3
SAY @6036 /* Dunno. Don't pay attention to politics. Nothing good down that road. */
IF ~~ EXIT
END

IF ~~ p.iron4
SAY @6037 /* Stop whoever's doing it. Otherwise, no. Un-corroding the iron would be like trying to un-burn wood. */
IF ~~ EXIT
END

IF ~~ p.iron5
SAY @6038 /* It's not a crop. It doesn't get diseases. Only thing that can make iron crumble like that is bad smiths or bad doings. */
IF ~~ EXIT
END

IF ~~ p.bathe
SAY @6039 /* Could you impose on me to take off my armour, put down my weapons and leave myself unable to do my job? */
= @381 /* No. */
IF ~~ EXIT
END

IF ~~ p.helmet
SAY @6040 /* For what? Luxury? If I'm not on guard, I'm not earning my keep. */
++ @6041 /* What keep? I'm not paying you a wage. */ + p.helmet1
++ @6042 /* I'm not going to drop dead because you relaxed for a few minutes. */ + p.helmet2
++ @6043 /* I order you to remove it. */ + p.helmet3
++ @6044 /* It seems like you're hiding something. */ + p.helmet4
END

IF ~~ p.helmet1
SAY @6045 /* Habit, then. */
IF ~~ EXIT
END

IF ~~ p.helmet2
SAY @6046 /* Maybe. Rather not test it. */
IF ~~ EXIT
END

IF ~~ p.helmet3
SAY @6047 /* No. I swore to protect, not obey. */
IF ~~ EXIT
END

IF ~~ p.helmet4
SAY @6048 /* My skull. From arrows. */
IF ~~ EXIT
END

IF ~~ p.beard
SAY @6049 /* Dwarves have beards. */
++ @6050 /* But you're a woman. */ + p.beard1
++ @6051 /* Even female dwarves? I didn't know that. */ + p.beard2
END

IF ~~ p.beard1
SAY @6049 /* Dwarves have beards. */
IF ~~ EXIT
END

IF ~~ p.beard2
SAY @6052 /* Wasn't that uncommon where I came from. When we leave dwarven territory, that's when the razors come out. Most other races can't handle bearded women. Spares you a lot of questions, a lot of awkwardness, and a lot of bad jokes. */
++ @6053 /* None of which bothers you, obviously. */ + p.beard3
++ @6054 /* Bah. Let them choke on it, I say. */ + p.beard4
++ @6055 /* *I* know how to handle a bearded woman. */ + p.beard5
++ @6056 /* Speaking of which, have you heard the one about- */ + p.beard6
END

IF ~~ p.beard3
SAY @6057 /* Gets annoying sometimes. Just happens less when you're holding a hammer. */
IF ~~ EXIT
END

IF ~~ p.beard4
SAY @4340 /* Likewise. */
IF ~~ EXIT
END


IF ~~ p.beard5
SAY @381 /* No. */
IF ~~ EXIT
END


IF ~~ p.beard6
SAY @1376 /* Yes. */
IF ~~ EXIT
END



IF ~~ p.mines
SAY @6058 /* If I asked someone to follow me into a "demon" infested hole, you think they'd do it? */
++ @6059 /* Of course they would. */ + p.mines1
++ @6060 /* Maybe if you presented yourself better. */ + p.mines2
++ @6061 /* Probably not, no. */ + p.mines3
END

IF ~~ p.mines1
SAY @6062 /* You've a bad eye for leadership. */
IF ~~ + p.mines4
END

IF ~~ p.mines2
SAY @6063 /* What you see is what you get. Prefer it that way. Nobody gets confused. */
IF ~~ + p.mines4
END

IF ~~ p.mines3
SAY @2624 /* Exactly. */
IF ~~ + p.mines4
END

IF ~~ p.mines4
SAY @6064 /* Some take orders, some give them. I'm a taker, and I don't play make-believe. */
IF ~~ EXIT
END


IF ~~ p.suggest
SAY @6065 /* Nope. */
++ @6066 /* Nothing at all? */ + p.suggest1
++ @6067 /*  Thank you. Your input is invaluable. */ + p.suggest1
END

IF ~~ p.suggest1
SAY @6068 /* You make decisions, I make sure they don't get you killed. */
IF ~~ EXIT
END


IF ~~ p.gold
SAY @6069 /* Below average. Like I said, crisis made life more expensive. Smiths charging more. Some think ironclad guards will just make them more of a target. */
= @6070 /* Besides, there's food and drink to cover. Mostly drink. */
++ @6071 /* How is it possible to drink that much gold away? */ + p.gold1
++ @6072 /* You'd be a lot wealthier for a little abstinence. */ + p.gold2
++ @6073 /* No long-term plans? No investments? */ + p.gold3
END

IF ~~ p.gold1
SAY @6074 /* Don't know many dwarves, do you? */
IF ~~ EXIT
END

IF ~~ p.gold2
SAY @6075 /* Wealth to spend on what? Better drink? Nothing worth splurging on in human bars. All just means to the end of making life more bearable. */
IF ~~ EXIT
END

IF ~~ p.gold3
SAY @6076 /* Don't like thinking long term. Too depressing. Makes my headache worse. */
IF ~~ EXIT
END

IF ~~ p.speech7
SAY @5312 /* When they spoke Common, yeah. Which was mostly just when they were teaching it to me, but still. Go tell them they don't talk like elves. I'm sure that'll go down a treat. */
IF ~~ EXIT
END


IF ~~ p.jobs
SAY @1941 /* What for? */
++ @6077 /* To get a better measure of who I'm working with. */ + p.jobs1
++ @6078 /* It sounds like you've done things I disagree with. I may not be able to travel with you in good conscience. */ + p.jobs2
++ @6079 /* I enjoy a good, bloody story. */ + p.jobs3
END

IF ~~ p.jobs1
SAY @6080 /* Not much in them to measure. */
IF ~~ + p.jobs4
END

IF ~~ p.jobs2
SAY @6081 /* Do you disagree with me fighting for you? Long as I'm here, that's all I'm doing. */
++ @6082 /* Even so, I think it'd best to hear about them. It'd give me a better measure of you. */ + p.jobs1
++ @6083 /* That's not the point. I can't shelter a criminal. */ + p.jobs2a
++ @6084 /* Stop stonewalling and tell me. */ + p.jobs2b
END

IF ~~ p.jobs2a
SAY @6085 /* You're not. I'm sheltering you. That's the arrangement. Besides, I could tell you a dozen stories and the most you could accuse me of is standing by. */
IF ~~ + p.jobs4
END

IF ~~ p.jobs2b
SAY @6086 /* I'm not stonewalling. Just not much to tell. */
IF ~~ + p.jobs4
END

IF ~~ p.jobs3
SAY @6087 /* Won't enjoy these, then. */
IF ~~ + p.jobs4
END

IF ~~ p.jobs4
SAY @6088 /* Lot of the time, nothing happens. I'm just a presence. People see me and don't do things they would've done otherwise. */
++ @6089 /* So tell me about a time when something did happen. */ + p.jobs5
++ @6090 /* That sounds boring. */ + p.jobs6
END

IF ~~ p.jobs5
SAY @6091 /* There was a fight. */
++ @311 /* And? */ + p.jobs7
++ @6092 /* What kind of fight? */ + p.jobs8
++ @6093 /* Did you win? */ + p.jobs9
END

IF ~~ p.jobs6
SAY @6094 /* Boring is good. I don't find trouble. I keep it from finding us. */
IF ~~ EXIT
END

IF ~~ p.jobs7
SAY @6095 /* Some people died. */
IF ~~ + p.jobs10
END

IF ~~ p.jobs8
SAY @6096 /* A short one. */
IF ~~ + p.jobs10
END

IF ~~ p.jobs9
SAY @1376 /* Yes. */
IF ~~ + p.jobs10
END

IF ~~ p.jobs10
SAY @6097 /* Don't know what you want me to say. Someone attacks my clients, I don't stop to ask who they are. */
++ @6098 /* Maybe you should try it. */ + p.jobs11
++ @6099 /* You're a very confusing person. */ + p.jobs12
++ @6100 /* Well. Since I'm your client now, it's good to know you won't be hesitating. */ + p.jobs13
++ @6101 /* Moidre, any mercenary would ask for some kind of information up front. Who they're going to be fighting, how many... */ + p.jobs14
END

IF ~~ p.jobs11
SAY @6102 /* Even if they feel like answering, there are three outcomes. They leave, they die, or I die. Doesn't matter in any of them. */
IF ~~ EXIT
END

IF ~~ p.jobs12
SAY @6103 /* Or you're not used to simplicity. */
IF ~~ EXIT
END

IF ~~ p.jobs13
SAY @6104 /* Right. You want to learn anything about me, let it be that. */
IF ~~ EXIT
END

IF ~~ p.jobs14
SAY @6105 /* Did I ask you? */
++ @640 /* ...no. */ + p.jobs15
++ @6106 /* You should have! */ + p.jobs16
END

IF ~~ p.jobs15
SAY @1719 /* Right. */
IF ~~ EXIT
END

IF ~~ p.jobs16
SAY @6107 /* But did I? */
IF ~~ EXIT
END


IF ~~ p.where
SAY @5896 /* Does it matter? */
++ @6108 /* It matters to me. */ + p.where1
++ @6109 /* No. I'm just curious. */ + p.where2
END

IF ~~ p.where1
SAY @6110 /* No it doesn't. My past isn't what protects you. */
IF ~~ + p.where3
END

IF ~~ p.where2
SAY @6111 /* Don't be. Nothing worth curiosity. */
IF ~~ + p.where3
END

IF ~~ p.where3
SAY @6112 /* You've watched me fight, and I'm not fighting you. You know what you need to. */
++ @6113 /* But how did you become a mercenary? And why? */ + p.where4
++ @6114 /* Good relationships aren't formed on a need-to-know basis. */ + p.where5
++ @6115 /* Fine. I wasn't that interested anyway. */ EXIT
END

IF ~~ p.where4
SAY @6116 /* Because I know how to fight. */
IF ~~ + p.where6
END

IF ~~ p.where5
SAY @6117 /* Don't need a good relationship to do my job. */
IF ~~ + p.where6
END

IF ~~ p.where6
SAY @6118 /* Ever read a book where they go on and on about the day's weather or the colour of someone's eyes? Ever catch yourself not caring? That's because it's not the story. It's details. Fat. */
= @6119 /* My book's got a lot of fat. The meat is, someone starts something, I hit them until it stops. The end. */
++ @6120 /* A little fat can add flavour, though. */ + p.where7
++ @6121 /* Why are you so guarded? */ + p.where8
++ @6122 /* Forget it. I don't know why I bothered. */ + p.where9
END

IF ~~ p.where7
SAY @6123 /* Not this fat. Rubber and gristle. */
IF ~~ EXIT
END

IF ~~ p.where8
SAY @6124 /* Job. */
IF ~~ EXIT
END

IF ~~ p.where9
SAY @6125 /* Me neither. */
IF ~~ EXIT
END

IF ~~ p.sullen
SAY @6126 /* Dwarf. */
IF ~~ EXIT
END

IF ~~ p.hair
SAY @6127 /* Wait till I'm not on contract. Buy myself a room, sturdy door. Lock it. Barricade it. Check the wardrobe and under the bed. Then a second or two with a sharp knife gets it done. Not like it has to be pretty. */
++ @6128 /* How much more neurotic can you get? */ + p.hair1
++ @6129 /* Damn. Here I was about to ask you for styling tips. */ + p.hair2
++ @6130 /* At that point, why even bother? */ + p.hair3
END

IF ~~ p.hair1
SAY @6131 /* Tried an invisibility potion once. Gets harder when you can't see what you're cutting, though. That left a mark. */
IF ~~ EXIT
END

IF ~~ p.hair2
SAY @6132 /* No amount of primping survives a helmet. Even I know that. */
IF ~~ EXIT
END

IF ~~ p.hair3
SAY @6133 /* When it's growing over your eyes, enough is enough. */
IF ~~ EXIT
END


IF ~~ p.travel
SAY @6134 /* People still out for your blood, far as I can tell. Job's not over yet. */
++ @6135 /* But I'm not paying you. There's no contract. What's keeping you here? */ + p.travel1
++ @6136 /* People could be out for my blood for the rest of my life. */ + p.travel2
++ @6137 /* I admire your dedication. */ + p.travel3
END

IF ~~ p.travel1
SAY @6138 /* Oath aside? Nothing. But nothing's pushing me away either. */
IF ~~ + p.travel4
END

IF ~~ p.travel2
SAY @6139 /* If they are, "the rest of your life" mightn't be long. */
IF ~~ + p.travel4
END

IF ~~ p.travel3
SAY @6140 /* Not dedication. Inertia. */
IF ~~ + p.travel4
END
END

CHAIN GV#MOIDJ p.travel4
@6141 /* My work's all the same. Doesn't matter who it's for. Always the same. Go somewhere, look scary, thump anything that doesn't think I'm scary enough. */
== GV#MOIDJ IF ~ReputationGT(Player1,7)~ THEN @6142 /* You haven't asked me to do anything ugly yet. Adventuring covers expenses. Don't fix what isn't broken. */
== GV#MOIDJ IF ~ReputationLT(Player1,8)~ THEN @6143 /* You're no worse than my other prospects. Adventuring covers expenses. Don't fix what isn't broken. */
EXIT


APPEND GV#MOIDJ

IF ~~ p.high
SAY @6144 /* Should I? */
= @6145 /* I'm a decent fighter. Might be proud of that if anything good ever came of it. Otherwise... */
++ @6146 /* What comes out of it is entirely up to you. */ + p.high1
++ @6147 /* You're an *excellent* fighter. */ + p.high2
++ @6148 /* If you worked hard to get good at something, you deserve to be proud. Who cares what comes of it? */ + p.high3
++ @6149 /* You're more than that. You're dependable, loyal, honest... */ + p.high4
++ @6150 /* Actually, I agree. You're a completely expendable grunt. I just wanted to remind you. */ + p.high5
END

IF ~~ p.high1
SAY @6151 /* Not that simple. Just because you think you're doing something doesn't mean you are. */
IF ~~ + p.high6
END

IF ~~ p.high2
SAY @6152 /* Could be the best fighter in the world. Makes no difference if the fight's crooked. */
IF ~~ + p.high6
END

IF ~~ p.high3
SAY @6153 /* I do. Rather be a bad fighter with a good cause than a good fighter with a bad one. But we don't get to choose what we are. */
IF ~~ + p.high6
END

IF ~~ p.high4
SAY @6154 /* Hah. Only because I haven't the sense to be anything else. */
IF ~~ EXIT
END

IF ~~ p.high5
SAY @6155 /* Hadn't forgotten. */
IF ~~ EXIT
END

IF ~~ p.high6
SAY @6156 /* You might say I'm doing good protecting you, but how would I know? Maybe those people after you are in the right. */
++ @6157 /* Do you really believe that? */ + p.high7
+ ~ReputationGT(Player1,13)~ + @6158 /* But I've done so many good things! */ + p.high8
+ ~ReputationLT(Player1,7)~ + @6159 /* Pfft. What do you mean "maybe"? */ + p.high9
++ @6160 /* If I have to worry about you siding with my enemies, you can leave right now. */ + p.high10
END

IF ~~ p.high7
SAY @6161 /* Dunno. Only got your word. That's my point. */
IF ~~ EXIT
END

IF ~~ p.high8
SAY @6162 /* Since we met. What about before? And why did you do them? No way of telling. */
IF ~~ EXIT
END

IF ~~ p.high9
SAY @6163 /* You being wrong doesn't make your enemies right. Sometimes they're just a different kind of wrong. */
IF ~~ EXIT
END

IF ~~ p.high10
SAY @6164 /* You don't. I swore an oath. That's what I go by. Only thing I can be sure of. */
IF ~~ EXIT
END

IF ~~ p.work
SAY @6165 /* Look for work. Or drink. Can usually do both in the same place, so it works out. */
++ @6166 /* What do you *like* to do, then? Apart from drink. */ + p.work1
++ @6167 /* Nobody can spend every waking hour either working or finding work. You'd go insane. There must be something else. */ + p.work2
++ @6168 /* Your life is dismal, isn't it? */ + p.work3
END

IF ~~ p.work1
SAY @6169 /* Not sure I even like that. More of an occupational necessity. */
IF ~~ EXIT
END

IF ~~ p.work2
SAY @6170 /* Insanity is doing the same thing over and over again and expecting different results. */
++ @1707 /* So? */ + p.work4
END

IF ~~ p.work3
SAY @6171 /* Finally noticed? */
IF ~~ EXIT
END

IF ~~ p.work4
SAY @6172 /* So maybe I am. */
IF ~~ EXIT
END


IF ~~ p.slave
SAY @324 /* Why? */
++ @6173 /* You were pretty upset by what we saw in the Cloakwood mines. */ + p.slave1
END

IF ~~ p.slave1
SAY @6174 /* People get upset by lots of things.  */
++ @6175 /* You don't. */ + p.slave2
++ @6176 /* But you seem to shrug everything else off. Only the slavery bothered you. */ + p.slave3
++ @6177 /* Are you being evasive? */ + p.slave4
END

IF ~~ p.slave2
SAY @6178 /* I do. You just don't notice. */
IF ~~ + p.slave4
END

IF ~~ p.slave3
SAY @6179 /* Shrug what off? The violence? Didn't always. Just got used to it. Show me enough slaves, I'll get used to that too. Not that I want to. */
IF ~~ + p.slave4
END

IF ~~ p.slave4
SAY @6180 /* I'm not a slave. Never have been. But I've seen slavery before and it shouldn't happen. That's all. */
IF ~~ EXIT
END

IF ~~ p.fist
SAY @6181 /* No. I was there. You didn't do it. */
IF ~~ + p.fist2
END

IF ~~ p.fist1
SAY @6182 /* Well. On one hand, you did kill Rieltar. Other hand, what we've seen, he had it coming. Either way, I've got an oath to keep. I'm doing my job, same as them. */
IF ~~ + p.fist2
END

IF ~~ p.fist2
SAY @6183 /* I know you don't deserve to die. Could be they don't either, but I'll take definitely over maybe. */
IF ~~ EXIT
END

IF ~~ p.bounce
SAY @6184 /* You're seeing things. Nobody bounces in armour, especially not dwarves. */
++ @6185 /* You're talking in complete sentences more often too. */ + p.bounce1
++ @6186 /* It's subtle, but by your standards you're practically skipping. */ + p.bounce2
++ @6187 /* You really shouldn't feed people's prejudices like that. */ + p.bounce3
IF ~~ EXIT
END

IF ~~ p.bounce1
SAY @6188 /* Soon fix that. */
IF ~~ EXIT
END

IF ~~ p.bounce2
SAY @6189 /* Armour's wearing in, maybe. Always get a bit more spry when it doesn't feel like a weight anymore. */
IF ~~ EXIT
END

IF ~~ p.bounce3
SAY @6190 /* Why not? If people assume you're always grumpy, they don't start asking questions when you actually are. "She's just being a dwarf." Saves a lot of time. */
IF ~~ EXIT
END

IF ~~ p.clan
SAY @6191 /* We pushed too hard, took too many. Someone was bound to notice. There'd been rumours about where the ore was coming from, and once the people we'd wronged came knocking, they stopped being rumours. */
= @6192 /* Half the clan supported our leader. Either didn't believe it or didn't care. Other half revolted. Half and half. Even sides. No winners. */
= @6193 /* In the end it was just me, him, and the last of the rebels beating down his door. I turned, yelled at him to run, that I'd hold them off, but he already had a dagger in his own heart. */
= @6194 /* They saw me standing over his body, and...I suppose they assumed I'd done it. Probably would have killed me otherwise. Probably would have let them.  */
IF ~~ EXIT
END



IF ~~ p.duergar
SAY @6195 /* Can't turn invisible, can't double in size. Probably can't see through illusions either, though I haven't dealt with enough mages to know. */
= @6196 /* That, and when a *half* duergar's born, a dwarf clan doesn't put it a sack and take it to the nearest river. Just barely, mind you. */
++ @6197 /* And what sets you apart from an everyday dwarf? */ + p.duergar1
++ @6198 /* That's a pretty bad deal. */ + p.duergar2
IF ~~ EXIT
END

IF ~~ p.duergar1
SAY @6199 /* Suppose I can hold my drink better, but that just means drinking's more expensive. Lightweights don't know how good they have it. */
IF ~~ + p.duergar3
END

IF ~~ p.duergar2
SAY @6200 /* Don't I know it. */
IF ~~ + p.duergar3
END

IF ~~ p.duergar3
SAY @6201 /* Only saving grace is I don't burn up in the sun. Least, no more than anyone who wears metal all day. */
IF ~~ EXIT
END


IF ~~ p.life
SAY @217 /* ... */
= @6202 /* Smithing? */
= @6203 /* No. Something else. Something without any hammering. */
= @6204 /* Gardening. */
++ @6205 /* You want to be a *gardener*? */ + p.life1
++ @6206 /* Oh. I was hoping for something exciting. */ + p.life2
++ @6207 /* What would you grow? */ + p.life3
END

IF ~~ p.life1
SAY @6208 /* I want to keep something alive without having to kill anyone to do it. */
IF ~~ + p.life3
END

IF ~~ p.life2
SAY @6209 /* What would you call exciting? Travelling? Fighting? Done enough of those. */
IF ~~ + p.life3
END

IF ~~ p.life3
SAY @6210 /* Hops, maybe. Get a little brewery going on the side. My own custom ales, for all the dwarves in human lands who can't find a decent drink anymore. Let the first batch go for free, as a toast to lost clans... */
= @6211 /* *sigh* Now you've got me thinking about what could have been. */
IF ~~ EXIT
END

IF ~~ p.recognise
SAY @6212 /* Only thing they'd remember is the helmet. I'll wear a different one. Don't want them knowing who I am up front, or they won't follow me. But once they're safe, I'll... */
= @6213 /* I'll tell them. Everything. They'd call me a hero otherwise, and I couldn't bear that. */
IF ~~ EXIT
END

IF ~~ p.soap
SAY @6214 /* You don't want it back. Trust me. I'll buy you a new one. */
IF ~~ EXIT
END

IF ~~ p.solonor2
SAY @5380 /* I won't bother preaching, then. But here's a story for you. */
= @5381 /* When I was around twelve, I heard about this ritual the priests have. Every turn of the moon, they mark an arrow with Solonor's symbol and shoot it into the sky. "To pierce the firmament and let the light of his teachings shine down on the faithful." Bit poncy, I know, but you have to do these things. So I thought I'd give it a go. */
= @5382 /* Only trouble is, whittling on a crossbow bolt is fiddly work. I was afraid if I didn't get it just right, Solonor would chuck the thing back at me and tell me I was rubbish. Must've ruined a dozen shafts trying to do it perfect. */
= @5383 /* By the time I'm finished, my hands are all bloody from where I nicked myself, and half the tribe's asked me what the hell I'm playing at. But I did it. And I shot it into the sky, like I was supposed to. And *just* as I did, a little sunray peeked through the clouds, right where I was standing. */
= @5384 /* That's the day I started believing. */
IF ~~ DO ~SetGlobal("GV#MOIDPidSolonorChitChat","GLOBAL",1)~ EXIT
END


IF ~~ p.beautiful
SAY @5385 /* Have they arse. I look like a boy dragged through a bush. And I don't approve of liars, so don't bother saying different. */
++ @5386 /* Nothing wrong with rugged boyishness. Nothing at all. */ + p.beautiful1
++ @5387 /* Alright, alright. Just trying to be nice. */ + p.beautiful2
END

IF ~~ p.beautiful1
SAY @5388 /* Oh. Well. Thanks, then. */
= @5389 /* *cough* */
IF ~~ EXIT
END

IF ~~ p.beautiful2
SAY @5390 /* What, and I've got so little respect for myself that I need you to fill in? Piss off. Tell me I have good aim next time. At least that'll be the truth. */
IF ~~ EXIT
END


IF ~~ p.father
SAY @5391 /* Oh, just...everything we could fit in a day. What have you been up to, what was my mum like, how'd you end up with that <CHARNAME> prat. */
= @5392 /* Heh, he told me he had a hell of a time getting the tribe to warm up to him. He just kept plaguing 'em and plaguing 'em until they were ready to smother him in honey and hang him up for the bears. */
= @5393 /* Then he got caught with 'em in a gnoll raid, ended up saving the chieftain's life. And that did it. I'd call it a tall tale, but that's about what it'd take to get a wild elf to trust you. Especially enough to...to take a baby off you. */
IF ~~ EXIT
END

IF ~~ p.mother
SAY @5394 /* Well. He said she was like me, and I asked what he meant by that. And he said "Like boiled leather. Rough, tough, doesn't bend easily." */
= @5395 /* I said "I didn't think you'd have liked that." And he said "She damned well made me." */
IF ~~ EXIT
END

IF ~~ p.plans
SAY @5396 /* I might finish my dad's book. Ain't sure I can write as pretty as he did, but I'll do my best. After that, who knows? I've got nothing tying me down, much like yourself. What do you reckon you'll do? */
++ @5397 /* I'll continue adventuring, naturally. */ + p.plans1
++ @5398 /* I'll find a home and a way to earn a crust. I really just want a normal life. */ + p.plans2
++ @5399 /* It might not be up to me. Something tells me I'm not in for a peaceful future. */ + p.plans3
++ @5400 /* I haven't thought about it. */ + p.plans4
END

IF ~~ p.plans1
SAY @5401 /* Fair enough, but you won't always be running into problems like this. There comes a time when "adventuring" is just wandering. */
IF ~~ EXIT
END

IF ~~ p.plans2
SAY @5402 /* Yeah, I wouldn't blame you. You never asked for any of this. */
IF ~~ + p.plans5
END

IF ~~ p.plans3
SAY @5403 /* Nah, things are bound to settle eventually. You keep stomping your enemies the way you are now, one day they'll decide you're best left alone. */
IF ~~ + p.plans5
END

IF ~~ p.plans4
SAY @5404 /* Me neither. Not as much as I should've, at least. I always thought I'd live and die with the tribe, but now... */
= @5405 /* It's a bit scary, isn't it? Having this much freedom. But it's a blessing, even then. Don't forget that. */
IF ~~ EXIT
END

IF ~~ p.plans5
SAY @5406 /* Soon, alright? Soon this'll be over and we'll find somewhere you can put your feet up. */
IF ~~ EXIT
END


IF ~~ p.bhaal
SAY @6215 /* Does that terminate our agreement? */
++ @2383 /* No, but- */ + p.bhaal1
++ @1376 /* Yes. */ + p.bhaal2
END

IF ~~ p.bhaal1
SAY @6216 /* Then I didn't need to know it. */
IF ~~ + p.bhaal3
END

IF ~~ p.bhaal2
SAY @6217 /* Fine. Let's make a new one. */
= @5519 /* Alak minn orn nagh mair. Take my life in your hands. Let me suffer where you would have suffered. Let me bleed where you would have bled. Let me fall where you would have fallen. */
IF ~~ + p.bhaal3
END

IF ~~ p.bhaal3
SAY @6218 /* You're the only person I've worked for who's worth a damn, <CHARNAME>. I'm not going anywhere unless that changes. */
IF ~~ EXIT
END

IF ~~ p.drink
SAY @6219 /* Always. I'm a dwarf, you know what to get. */
IF ~~ EXIT
END

IF ~~ p.drink2
SAY @6220 /* Do you? Think it's time I got some rounds in. */
IF ~~ EXIT
END

IF ~~ p.girdle
SAY @6221 /* Girdle? */
IF ~~ EXIT
END


//NPC Specific PIDS


//Ajantis

IF ~~ p.ajantis
SAY @6222 /* It's what I was taught. Don't know about everyone else. */
++ @6223 /* You're worth more than your shield, Moidre. */ + p.ajantis1
++ @6224 /* Well, it certainly worked for you. */ + p.ajantis2
++ @6225 /* Could you teach me as well? */ + p.ajantis3
++ @6226 /* It all sounds like hogwash to me. */ + p.ajantis4
END

IF ~~ p.ajantis1
SAY @6227 /* Am I? If someone switched me with a dwarf-shaped golem, would you notice? */
++ @6228 /* Yes. I would. */ + p.ajantis5
++ @6229 /* It'd probably be more talkative. */ + p.ajantis6
++ @6230 /* I might not, honestly. */ + p.ajantis7
END

IF ~~ p.ajantis2
SAY @6231 /* No, it works for other people. That's the point. */
IF ~~ EXIT
END

IF ~~ p.ajantis3
SAY @6232 /* No. If you do what I do, it defeats the purpose. */
IF ~~ EXIT
END

IF ~~ p.ajantis4
SAY @6233 /* 'Course it does. You keep yourself alive, I keep other people alive. Different jobs, different techniques. */
IF ~~ EXIT
END

IF ~~ p.ajantis5
SAY @6234 /* Only at mealtime. */
IF ~~ EXIT
END

IF ~~ p.ajantis6
SAY @2176 /* Eh. */
IF ~~ EXIT
END

IF ~~ p.ajantis7
SAY @6235 /* Right. My line of work, it doesn't matter who you are. Just how you fight. */
IF ~~ EXIT
END



//Alora

IF ~~ p.alora
SAY @6236 /* Ehh. Throw enough crap at a wall, something'll stick. */
= @6237 /* Besides, she had an advantage. Nobody's ever tried to do it before.. */
IF ~~ EXIT
END


//Imoen

IF ~~ p.imoen
SAY @6238 /* Yes. Not that it makes a difference anymore. */
IF ~~ EXIT
END


//Jaheira

IF ~~ p.jaheira
SAY @6239 /* Not literally. Don't rule it out, though. Mages are tricky. */
IF ~~ EXIT
END


//Minsc

IF ~~ p.minsc
SAY @6240 /* Gonna hurt even more if he has to make a sacrifice he's not prepared for. Not everything always works out for the best. In fact, it usually doesn't. Best break him in gently. */
IF ~~ EXIT
END


//Montaron

IF ~~ p.montaron
SAY @6241 /* Don't trust much of anyone. He just happened to bring it up. */
= @6242 /* I'd say boot him, but if you've any sense, you've already thought about it.  */
++ @6243 /* He's treacherous, but he's not stupid. He won't turn on us as long as our goals are the same. */ + p.montaron1
++ @6244 /* I'd rather have him where I can see him. */ + p.montaron2
++ @6245 /* So far I've got no more reason to suspect him than you. */ + p.montaron3
++ @6246 /* I haven't, actually. */ + p.montaron4
END

IF ~~ p.montaron1
SAY @6247 /* Sooner or later they won't be the same. Wouldn't count on him telling you when. */
IF ~~ EXIT
END

IF ~~ p.montaron2
SAY @6248 /* I don't need to know the reason. Long as you have one. */
IF ~~ EXIT
END

IF ~~ p.montaron3
SAY @6249 /* Wrong. I might be working for someone else. He's definitely working for someone else. */
IF ~~ EXIT
END

IF ~~ p.montaron4
SAY @6250 /* Do. I've got eyes in the back of my head, but the helmet's in the way. */
IF ~~ EXIT
END


//neera

IF ~~ p.neera
SAY @6251 /* Not threatening. Warning. */
= @6252 /* I don't want to kill her. That's why I told her, so she'd know to run. */
IF ~~ EXIT
END


//Rasaad

IF ~~ p.rasaad
SAY @6253 /* Eh. Still dubious. */
= @6254 /* "None who would embrace Selune are turned away." Pretty words, but nothing's tested them yet.  He had one good thing happen to him and forgot how many bastards there are in the world. */
IF ~~ EXIT
END


//Safana

IF ~~ p.safana
SAY @217 /* ... */
= @6255 /* Joking, sincere, don't care. Ignoring it. */
IF ~~ EXIT
END

//Sharteel

IF ~~ p.sharteel
SAY @6256 /* Back where I lived, one of the ollams said there were two things every dwarf should have a right to. The hair on their chins, and the support of their clan. */
= @6257 /* I've only got one. Have to hold on to it. */
IF ~~ EXIT
END

//Skie

IF ~~ p.skie
SAY @6258 /* No doubt. But she'll find out eventually. Then she'll know to listen next time. */
IF ~~ EXIT
END


//Viconia

IF ~~ p.viconia
SAY @6259 /* I know what it's like to feel you don't have a place. */
= @6260 /* She might deserve a little help. Might not. Dunno what she did before she came topside. I do know I wouldn't wish that feeling on most people, and she's not made herself an exception yet. */
IF ~~ EXIT
END

//Xan

IF ~~ p.xan
SAY @6261 /* Nah. Jostled something, I think. Went almost a whole evening afterwards without saying the word "doomed." New record for him. */
IF ~~ EXIT
END

//Yeslick

IF ~~ p.yeslick
SAY @6262 /* Not important. You just need something to call me. You won't meet another Moidre, so Moidre will do. */
IF ~~ EXIT
END





// Unhappy PIDs


IF ~IsGabber(Player1) GlobalGT("GV#MOIDTalk","GLOBAL",2) Global("GV#MOIDUnhappy","GLOBAL",1)~ GV#MOIDPIDUnhappy
SAY @217 /* ... */
++ @3586 /* How are you? */ + p1.1
++ @6263 /* What do you think we should do next? */ + p2.unhappy
++ @6264 /* Still sulking, I see. */ + p.sulk
++ @6265 /* I can't help noticing you're not fighting as hard. */ + p.fight
++ @3624 /* Never mind. */ EXIT
END

IF ~~ p2.unhappy
SAY @6266 /* Don't care. */
IF ~~ EXIT
END

IF ~~ p.sulk
SAY @6267 /* Is there a fight? */
++ @6268 /* Not right at this moment, no. */ + p.sulk1
++ @6269 /* There's no reason we can't still be friends. */ + p.sulk2
++ @6270 /* There might be if this keeps up. */ + p.sulk3
END

IF ~~ p.sulk1
SAY @6271 /* Nothing to talk about, then. */
IF ~~ EXIT
END

IF ~~ p.sulk2
SAY @6272 /* "Still." */
IF ~~ EXIT
END

IF ~~ p.sulk3
SAY @6273 /* I'll be ready for it. */
IF ~~ EXIT
END


IF ~~ p.fight
SAY @538 /* Yeah. */
++ @6274 /* May I ask why? */ + p.fight1
++ @6275 /* Perhaps we could do something about that? */ + p.fight2
++ @6276 /* Get your act together or you're gone. */ + p.fight3
END

IF ~~ p.fight1
SAY @6277 /* I'm tired. */
IF ~~ EXIT
END

IF ~~ p.fight2
SAY @6278 /* You can't. */
IF ~~ EXIT
END

IF ~~ p.fight3
SAY @6279 /* Bad incentive. */
IF ~~ EXIT
END
END
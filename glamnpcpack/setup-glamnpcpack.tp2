BACKUP ~weidu_external/GlamNPCPack/backup~
SUPPORT ~https://www.gibberlings3.net/forums/forum/186-glams-npc-pack/~

ALWAYS

  ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN
    FAIL ~DLC Merger is required before mods can be installed on this game. Check the readme for more information and a link to download DLC Merger.~
  END
  
  ACTION_IF !VARIABLE_IS_SET cd_glamnpcpack_always THEN BEGIN // check to make this happen only once per install 
      
    OUTER_SET cd_glamnpcpack_always = 1
    
    ACTION_IF GAME_IS ~bgee eet~ BEGIN
      
      ACTION_IF GAME_IS ~bgee~ BEGIN
        /*Tell the player it is using BG:EE stuff */
        PRINT @9 /* BG:EE install detected */
        INCLUDE ~GlamNPCPack\Libraries\g3_bgee_cpmvars.tpa~
      END ELSE BEGIN // eet  
        /*Tell the player it is using EET stuff */
        PRINT @12 /* EET install detected */
        INCLUDE ~EET/other/cpmvars/eet_cpmvars.tpa~
      END 

      OUTER_SET enhanced_edition = 1
    
      OUTER_SPRINT ~tra_location~ ~glamnpcpack/languages~
      
    END ELSE BEGIN   

      ACTION_IF GAME_IS ~tutu tutu_totsc~ BEGIN
        /* Tell the player it is using Tutu stuff */
        PRINT @10 /* Tutu install detected. */
        INCLUDE ~GlamNPCPack\Libraries\g3_tutu_cpmvars.tpa~
      END ELSE ACTION_IF GAME_IS ~bgt~ THEN BEGIN
        /* Tell the player it is using BGT stuff */
        PRINT @11 /* BGT install detected. */
        INCLUDE ~GlamNPCPack\Libraries\g3_bgt_cpmvars.tpa~
      END ELSE BEGIN
        /* Tell the player it is not Tutu or BGT */
        FAIL @13 /* Please install on BG:EE, Tutu, BGT or EET. */
      END

      OUTER_SET enhanced_edition = 0
    
      OUTER_SPRINT ~tra_location~ ~weidu_external/glamnpcpack/languages~

      // convert strings from UTF-8 for originals
      ACTION_DEFINE_ARRAY cdnoconvert BEGIN weidu ee END
      ACTION_DEFINE_ARRAY cdreload BEGIN game END
      LAF HANDLE_CHARSETS INT_VAR from_utf8 = 1 infer_charsets = 1
                          STR_VAR default_language = ~english~ tra_path = ~glamnpcpack/languages~ out_path = ~weidu_external/glamnpcpack/languages~ noconvert_array = cdnoconvert reload_array = cdreload END
      
    END
    
    INCLUDE ~GlamNPCPack/Libraries/cd_portrait_copy.tph~

  END

END  

LANGUAGE ~English~ ~English~ 
  ~GlamNPCPack/Languages/English/game.tra~
  ~GlamNPCPack/Languages/English/weidu.tra~

BEGIN @14 /* Glam's NPC Pack for BG:EE, BGT and BG TUTU */
LABEL ~gv#_glams_npc_pack~

OUTER_SPRINT "ElfsongBartender" ~BART8~
OUTER_SPRINT "GhostKnight" ~%tutu_scriptbg%KNIGHT%eet_var%~

ACTION_IF enhanced_edition BEGIN 

  // BAELOTH BANTER
  PRINT @15 /* BG:EE detected - Baeloth interdia.2da append - set Baeloth's banter file (BBAELOTH) */
  APPEND ~interdia.2da~ ~BAELOTH BBAELOTH~  UNLESS ~BAELOTH~
  ACTION_IF NOT (FILE_EXISTS_IN_GAME ~BBAELOTH.dlg~) THEN BEGIN
    COMPILE EVALUATE_BUFFER ~GlamNPCPack/dialogue/BBAELOTHFix.d~
  END
  
  OUTER_SPRINT "BlackguardKitCheck" ~!Kit(Player1,Blackguard)~ /* !Kit(Player1,Blackguard) */
  
  COPY ~GlamNPCPack/Creature/GV#DAVE.cre~ ~override/GV#DAVE.cre~
       ~GlamNPCPack/Creature/GV#FLAR.cre~ ~override/GV#FLAR.cre~
       ~GlamNPCPack/Creature/GV#MOID.cre~ ~override/GV#MOID.cre~
       ~GlamNPCPack/Creature/gv#vynd.cre~ ~override/gv#vynd.cre~
  
END ELSE BEGIN   

  APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~

  // Fixing Imoen's dialogue file
  APPEND ~interdia.2da~ ~IMOEN %IMOEN_BANTER%~ UNLESS ~%IMOEN_BANTER%~
  ACTION_IF NOT (FILE_EXISTS_IN_GAME ~%IMOEN_BANTER%.dlg~) AND (GAME_IS ~tutu tutu_totsc~) THEN BEGIN
    COMPILE EVALUATE_BUFFER ~GlamNPCPack/dialogue/bimoen.d~ 
  END
  
  /* ACTION.IDS and TRIGGER.IDS patching to ToB - thanks, Cam, if you read it */
  /* and GTIMES.IDS, ANIMATE.IDS patching to ToB, courtesy of the BG2 Fix Pack */
  INCLUDE ~GlamNPCPack/Libraries/tob2soa.tph~
  INCLUDE ~GlamNPCPack/Libraries/ids_entries_cleanup.tph~

  /* Tutu Area Script Assignment Patching: All Areas Script ID'd */
  ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN
    INCLUDE ~GlamNPCPack/Libraries/tutu_area_script_assign.tph~
  END

  /* Area Type Flagging */
  /* ToSC only: Tutu and BGT */
  ACTION_IF (FILE_EXISTS_IN_GAME ~FW1500.are~) OR (FILE_EXISTS_IN_GAME ~ARW500.are~) THEN BEGIN // if TotSC is installed
    COPY_EXISTING ~%IsleofBalduranN%.are~ ~override~
                  ~%IsleofBalduranS%.are~ ~override~
                  ~%DurlagsTower%.are~ ~override~
                  ~%Farmlands%.are~ ~override~
      READ_BYTE  "0x48" "flags"
      WRITE_BYTE "0x48" ("%flags%" BOR "0b00010001")
    BUT_ONLY_IF_IT_CHANGES
  END

    /* FOREST and OUTDOOR: Tutu and BGT */
  ACTION_IF GAME_IS ~tutu tutu_totsc bgt~ THEN BEGIN
    COPY_EXISTING ~%FishingVillage%.are~ ~override~
                  ~%Peldvale%.are~ ~override~
                  ~%LionsWay%.are~ ~override~
                  ~%CoastWay%.are~ ~override~
                  ~%Larswood%.are~ ~override~
                  ~%ShipwrecksCoast%.are~ ~override~
                  ~%HighHedge%.are~ ~override~
                  ~%MutaminsGarden%.are~ ~override~
                  ~%Lighthouse%.are~ ~override~
                  ~%RedCanyons%.are~ ~override~
                  ~%SouthBeregostRoad%.are~ ~override~
                  ~%Ulcaster%.are~ ~override~
                  ~%ArchaeologicalSite%.are~ ~override~
                  ~%FishermansLake%.are~ ~override~
                  ~%NorthNashkelRoad%.are~ ~override~
                  ~%LonelyPeaks%.are~ ~override~
                  ~%FirewineBridge%.are~ ~override~
                  ~%BearRiver%.are~ ~override~
                  ~%ValleyoftheTombs%.are~ ~override~
                  ~%DryadFalls%.are~ ~override~
                  ~%FireLeafForest%.are~ ~override~
                  ~%GibberlingMountains%.are~ ~override~
      READ_BYTE  "0x48" "flags"
      WRITE_BYTE "0x48" ("%flags%" BOR "0b00010001")
      BUT_ONLY_IF_IT_CHANGES

  /* OUTDOOR ONLY: Tutu and BGT */
  COPY_EXISTING ~%GnollStronghold%.are~ ~override~
                ~%NashkelMines%.are~ ~override~
                ~%FriendlyArmInn%.are~ ~override~
                ~%Temple%.are~ ~override~
                ~%NashkelCarnival%.are~ ~override~
    READ_BYTE  "0x48" "flags"
    WRITE_BYTE "0x48" ("%flags%" BOR "0b00000001")
    BUT_ONLY_IF_IT_CHANGES

  /* CITY and OUTDOOR */
  COPY_EXISTING ~%WyrmsCrossing%.are~ ~override~
                ~%Candlekeep_Ch6%.are~ ~override~
                ~%Gullykin%.are~ ~override~
    READ_BYTE  "0x48" "flags"
    WRITE_BYTE "0x48" ("%flags%" BOR "0b00001001")
    BUT_ONLY_IF_IT_CHANGES
  END
  
  OUTER_SPRINT "BlackguardKitCheck" ""
  ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN
    OUTER_SPRINT "ElfsongBartender" ~_BART11~
  END
  
  COPY ~GlamNPCPack/Creature/GV#DAVE2.cre~ ~override/GV#DAVE.cre~
       ~GlamNPCPack/Creature/GV#FLAR2.cre~ ~override/GV#FLAR.cre~
       ~GlamNPCPack/Creature/GV#MOID2.cre~ ~override/GV#MOID.cre~
       ~GlamNPCPack/Creature/gv#vynd2.cre~ ~override/gv#vynd.cre~

END

//AUDIO

LAF HANDLE_AUDIO END




//CREATURES

COPY_EXISTING ~GV#DAVE.cre~ ~override~ // copied from gv#dave or gv#dave2 in ee/non-ee section, above

  SAY NAME1 @24 /* Dave */
  SAY NAME2 @24 /* Dave */

  SAY BIO @25 /* When asked about his past, Dave chuckles and says it's not the past you should be interested in, but the present. Born to a single mother in a quiet village, Dave seems to have joined the church of Talos on a whim, for there was no outside force, nor troubled childhood, nor deranged power-hunger to compel him. Despite this, he expresses no doubts or regrets over his chosen path, and refuses to be swayed from it. */

  SAY LEADER @26 /* *bewildered laughter* */
  SAY TIRED @27 /* I'm beat. */
  SAY BORED @28 /* Isn't there something better we could be doing? */
  SAY BATTLE_CRY1 @29 /* You're gonna regret this! */
  SAY BATTLE_CRY2 @29 /* You're gonna regret this! */
  SAY BATTLE_CRY3 @29 /* You're gonna regret this! */
  SAY BATTLE_CRY4 @29 /* You're gonna regret this! */
  SAY BATTLE_CRY5 @29 /* You're gonna regret this! */
  SAY DAMAGE @30 /* [GV#DAVEl] */
  SAY DYING @31 /* [GV#DAVEm] */
  SAY HURT @32 /* Help me! I'm injured! */
  SAY SELECT_COMMON1 @33 /* Yes? */
  SAY SELECT_COMMON2 @34 /* What can I do? */
  SAY SELECT_COMMON3 @35 /* You need something? */
  SAY SELECT_ACTION1 @36 /* I'll get on it. */
  SAY SELECT_ACTION2 @37 /* Now's better than later. */
  SAY SELECT_ACTION3 @38 /* Got it. */
  PATCH_IF enhanced_edition BEGIN 
    LPF ADD_CRE_EFFECT INT_VAR opcode = 360 target = 1 timing = 1 END
  END  

COPY ~GlamNPCPack/Creature/GV#ALAN.cre~ ~override/GV#ALAN.cre~
  SAY NAME1 @39 /* Alan */
  SAY NAME2 @39 /* Alan */
  SAY DAMAGE @40 /* [BELTT08] */
  SAY DYING @41 /* [BELTT09] */

COPY ~GlamNPCPack/Creature/GV#BRIA.cre~ ~override/GV#BRIA.cre~
  SAY NAME1 @42 /* Brian */
  SAY NAME2 @42 /* Brian */
  SAY DAMAGE @43 /* [VOLO08] */
  SAY DYING @44 /* [VOLO09] */

COPY ~GlamNPCPack/Creature/GV#JEFF.cre~ ~override/GV#JEFF.cre~
  SAY NAME1 @45 /* Stormlord Jeff */
  SAY NAME2 @45 /* Stormlord Jeff */
  SAY DAMAGE @46 /* [DAVEO08] */
  SAY DYING @47 /* [DAVEO09] */

COPY ~GlamNPCPack/Creature/GV#KELL.cre~ ~override/GV#KELL.cre~
  SAY NAME1 @48 /* Kelly */
  SAY NAME2 @48 /* Kelly */
  SAY DAMAGE @49 /* [SHOAL03] */
  SAY DYING @50 /* [SHOAL04] */

COPY ~GlamNPCPack/Creature/GV#KELLa.cre~ ~override/GV#KELLa.cre~
  SAY NAME1 @48 /* Kelly */
  SAY NAME2 @48 /* Kelly */
  SAY DAMAGE @49 /* [SHOAL03] */
  SAY DYING @50 /* [SHOAL04] */

COPY ~GlamNPCPack/Creature/GV#PAM.cre~ ~override/GV#PAM.cre~
  SAY NAME1 @51 /* Pam */
  SAY NAME2 @51 /* Pam */
  SAY DAMAGE @52 /* [KRYST08] */
  SAY DYING @53 /* [KRYST09] */

COPY ~GlamNPCPack/Creature/GV#PHIL.cre~ ~override/GV#PHIL.cre~
  SAY NAME1 @54 /* Phil */
  SAY NAME2 @54 /* Phil */
  SAY DAMAGE @55 /* [THALT03] */
  SAY DYING @56 /* [THALT04] */

COPY ~GlamNPCPack/Creature/GV#STOR.cre~ ~override/GV#STOR.cre~
  SAY NAME1 @57 /* Storm Knight */
  SAY NAME2 @57 /* Storm Knight */
  SAY DAMAGE @58 /* [GUARR05] */
  SAY DYING @59 /* [GUARR06] */

COPY ~GlamNPCPack/Creature/GV#TARA.cre~ ~override/GV#TARA.cre~
  SAY NAME1 @60 /* Taranis */
  SAY NAME2 @60 /* Taranis */
  SAY INITIAL_MEETING @61 /* Fear my wrath, for it is great indeed. */
  SAY DAMAGE @62 /* [SCARR08] */
  SAY DYING @63 /* [SCARR09] */

COPY ~GlamNPCPack/Creature/GV#THUN.cre~ ~override/GV#THUN.cre~
  SAY NAME1 @64 /* Thundercat */
  SAY NAME2 @64 /* Thundercat */

COPY ~GlamNPCPack/Creature/GV#THUN2.cre~ ~override/GV#THUN2.cre~
  SAY NAME1 @64 /* Thundercat */
  SAY NAME2 @64 /* Thundercat */

COPY_EXISTING ~GV#FLAR.cre~ ~override~ // copied from gv#flar or gv#flar2 in ee/non-ee section, above

  SAY NAME1 @65 /* Flara */
  SAY NAME2 @65 /* Flara */

  SAY BIO @66 /* Before you even had the chance to ask about her past, Flara insisted she was once a powerful red dragon, who is now trapped in human form. This may be true, or it may not; she can offer no concrete explanation for how such a predicament came about, nor does she recall much of her time before it. Nevertheless, the air around her ripples with heat, and her fiery breath is difficult to argue with, though not as much as her temperament. */

  SAY MORALE @67 /* I leave you to handle this! */
  SAY LEADER @68 /* It is only right that I lead. */
  SAY TIRED @69 /* Are we to march till we drop? I must have rest! */
  SAY BORED @70 /* If I stand still much longer, I shall sprout roots. */
  SAY BATTLE_CRY1 @71 /* Die, fool! */
  SAY BATTLE_CRY2 @71 /* Die, fool! */
  SAY BATTLE_CRY3 @71 /* Die, fool! */
  SAY BATTLE_CRY4 @71 /* Die, fool! */
  SAY BATTLE_CRY5 @71 /* Die, fool! */
  SAY DAMAGE @72 /* [gv#flarl] */
  SAY DYING @73 /* [gv#flarm] */
  SAY HURT @74 /* I'm hurt! Help me, you fools! */
  SAY SELECT_COMMON1 @75 /* What? */
  SAY SELECT_COMMON2 @76 /* What is it? */
  SAY SELECT_COMMON3 @77 /* I'm listening. */
  SAY SELECT_ACTION1 @78 /* I'll do it. */
  SAY SELECT_ACTION2 @79 /* So be it. */
  SAY SELECT_ACTION3 @80 /* If I must. */
  PATCH_IF enhanced_edition BEGIN 
    LPF ADD_CRE_EFFECT INT_VAR opcode = 360 target = 1 timing = 1 END
  END  

COPY ~GlamNPCPack/Creature/GV#SIGU.cre~ ~override/GV#SIGU.cre~
  SAY NAME1 @81 /* Sigurd */
  SAY NAME2 @81 /* Sigurd */
  SAY INITIAL_MEETING @82 /* Troubled times are upon us. */
  SAY DAMAGE @83 /* [ELFFF05] */
  SAY DYING @84 /* [ELFFF06] */

COPY ~GlamNPCPack/Creature/GV#KRAK.cre~ ~override/GV#KRAK.cre~
  SAY NAME1 @85 /* Lord Burnheart */
  SAY NAME2 @85 /* Lord Burnheart */
  SAY DAMAGE @86 /* [GREYW08] */
  SAY DYING @87 /* [GREYW09] */

COPY ~GlamNPCPack/Creature/GV#MARI.cre~ ~override/GV#MARI.cre~
  SAY NAME1 @88 /* Lady Burnheart */
  SAY NAME2 @88 /* Lady Burnheart */
  SAY DAMAGE @89 /* [FTOWN05] */
  SAY DYING @90 /* [FTOWN06] */

COPY ~GlamNPCPack/Creature/GV#WINK.cre~ ~override/GV#WINK.cre~
  SAY NAME1 @91 /* Reeves */
  SAY NAME2 @91 /* Reeves */
  SAY DAMAGE @92 /* [BASSL03] */
  SAY DYING @93 /* [BASSL04] */

COPY ~GlamNPCPack/Creature/GV#LITT.cre~ ~override/GV#LITT.cre~

  SAY NAME1 @94 /* Littlun */
  SAY NAME2 @94 /* Littlun */

  SAY BIO @95 /* When asked about her past, Littlun proudly informs you of how she was taken in as a baby by a tribe of elves, who raised her as one of their own. However, while she is very much a product of the wilds, the mechanical, rapid-fire crossbow she carries is the work of her long-deceased father, once a master craftsman and wandering scholar. She is a walking contradiction, a juxtaposition of untamed nature and advanced technology.  */

  SAY LEADER @96 /* Yeah, I'll take point. Saves me having to shoot past you lot. */
  SAY TIRED @97 /* I'm used to trekking, but this is just torture. Let's bed down. */
  SAY BORED @98 /* Are we gonna get a move on, or what? */
  SAY BATTLE_CRY1 @99 /* Time for some target practice! */
  SAY BATTLE_CRY2 @99 /* Time for some target practice! */
  SAY BATTLE_CRY3 @99 /* Time for some target practice! */
  SAY BATTLE_CRY4 @99 /* Time for some target practice! */
  SAY BATTLE_CRY5 @99 /* Time for some target practice! */
  SAY DAMAGE @100 /* [GV#LITTl] */
  SAY DYING @101 /* Look after the crossbow... */
  SAY HURT @102 /* Make mine a healing potion... */
  SAY SELECT_COMMON1 @103 /* Need a hand? */
  SAY SELECT_COMMON2 @104 /* Ready for action. */
  SAY SELECT_COMMON3 @105 /* Just say the word. */
  SAY SELECT_ACTION1 @106 /* Righto. */
  SAY SELECT_ACTION2 @107 /* Easy enough. */
  SAY SELECT_ACTION3 @108 /* Give us a sec. */
  SAY CRITICAL_HIT @109 /* Hah! Right between the eyes! */
  SAY CRITICAL_MISS @110 /* Ugh, that's not like me... */
  SAY TARGET_IMMUNE @111 /* I ain't making a dent here! */
  SAY HIDDEN_IN_SHADOWS @112 /* They won't catch a whiff of me. */
  SAY SPELL_DISRUPTED @113 /* Argh, my spell's gone tits up! */


COPY ~GlamNPCPack/Creature/GV#PIER.cre~ ~override/GV#PIER.cre~
  SAY NAME1 @114 /* Pierson */
  SAY NAME2 @114 /* Pierson */
  SAY DAMAGE @115 /* [PNOBL10] */
  SAY DYING @116 /* [PNOBL11] */


COPY_EXISTING ~GV#MOID.cre~ ~override~ // copied from gv#moid or gv#moid2 in ee/non-ee section, above

  SAY NAME1 @117 /* Moidre */
  SAY NAME2 @117 /* Moidre */

  SAY BIO @118 /* When asked about her past, Moidre says little, which you've quickly come to realise is normal for her. Her helmet, which she refuses to remove even when ordered, hides her countenance from the world, and her voice rarely wavers from its flat inflection, making her as easy to engage with as a brick; however, her intense discipline marks her as something more than common sellsword. */

  SAY LEADER @119 /* An honour. */
  SAY TIRED @120 /* *yawn* */
  WRITE_LONG BORED "-1"
  WRITE_LONG BATTLE_CRY1 "-1"
  WRITE_LONG BATTLE_CRY2 "-1"
  WRITE_LONG BATTLE_CRY3 "-1"
  WRITE_LONG BATTLE_CRY4 "-1"
  WRITE_LONG BATTLE_CRY5 "-1"
  SAY DAMAGE @121 /* [GV#MOIDl] */
  SAY DYING @122 /* [GV#MOIDm] */
  SAY HURT @123 /* Ignore the pain. */
  SAY SELECT_COMMON1 @124 /* What? */
  SAY SELECT_COMMON2 @125 /* Huh? */
  SAY SELECT_COMMON3 @126 /* Speak. */
  SAY SELECT_ACTION1 @127 /* As you wish. */
  SAY SELECT_ACTION2 @128 /* Done. */
  SAY SELECT_ACTION3 @129 /* Fine. */
  PATCH_IF enhanced_edition BEGIN 
    LPF ADD_CRE_EFFECT INT_VAR opcode = 360 target = 1 timing = 1 END
  END  

COPY_EXISTING ~GV#vynd.cre~ ~override~ // copied from gv#vynd or gv#vynd2 in ee/non-ee section, above

  SAY NAME1 @130 /* Vynd */
  SAY NAME2 @130 /* Vynd */

  SAY BIO @131 /* When asked about his past, Vynd wonders aloud how you'd feel if some nosy bastard you barely know asked for your life story. You've pieced together that he's not new to the surface world, and he left the Underdark by choice. Beyond that, he remains a mystery. A snide, abrasive mystery. */

  SAY MORALE @132 /* A dead man serves no-one. */
  SAY LEADER @133 /* I'll be the leader for now. Just, uh, don't stand between me and the exit. */
  SAY TIRED @134 /* I'm tired of trotting around like a damn mule. Let's find an inn and some warm beds. */
  SAY BORED @135 /* There must be more to life than standing around wondering if there's more to life. */
  SAY BATTLE_CRY1 @136 /* My enemies always bring out the best in me. */
  SAY BATTLE_CRY2 @136 /* My enemies always bring out the best in me. */
  SAY BATTLE_CRY3 @136 /* My enemies always bring out the best in me. */
  SAY BATTLE_CRY4 @136 /* My enemies always bring out the best in me. */
  SAY BATTLE_CRY5 @136 /* My enemies always bring out the best in me. */
  SAY DAMAGE @137 /* [gv#vyndl] */
  SAY DYING @138 /* [gv#vyndm] */
  SAY HURT @139 /* I think my adventuring days are almost over. */
  SAY SELECT_COMMON1 @140 /* Yeah? */
  SAY SELECT_COMMON2 @141 /* What's up? */
  SAY SELECT_COMMON3 @142 /* What now? */
  SAY SELECT_ACTION1 @143 /* Here I go. */
  SAY SELECT_ACTION2 @144 /* On my way. */
  SAY SELECT_ACTION3 @145 /* No worries. */ 
  PATCH_IF enhanced_edition BEGIN 
    LPF ADD_CRE_EFFECT INT_VAR opcode = 360 target = 1 timing = 1 END
  END  

COPY ~GlamNPCPack/Creature/GV#SPID.cre~ ~override/GV#SPID.cre~

  SAY NAME1 @146 /* Spider */
  SAY NAME2 @146 /* Spider */

COPY ~GlamNPCPack/Creature/GV#CORPS.cre~ ~override/GV#CORPS.cre~
  SAY NAME1 @147 /* Corpse */
  SAY NAME2 @147 /* Corpse */



//DIALOGUE
COMPILE EVALUATE_BUFFER ~GlamNPCPack/Dialogue~
ACTION_IF enhanced_edition THEN BEGIN
  COMPILE EVALUATE_BUFFER ~GlamNPCPack/BGEE~
END
  
// Glam-Sandrah
ACTION_IF FILE_EXISTS_IN_GAME ~BGV#LITT.dlg~ AND FILE_EXISTS_IN_GAME ~BSandr.dlg~ THEN BEGIN
  PRINT @148 /* Adding crossmod banters between Glam's NPC Pack and Sandrah */
  COMPILE EVALUATE_BUFFER ~GlamNPCPack/crossmod/sandrah~
END


//JOURNAL
ADD_JOURNAL @1 @2 @3 @4 @5 @6 @7 @8

//SCRIPTS
COMPILE EVALUATE_BUFFER ~GlamNPCPack/Script/compile~

ACTION_BASH_FOR ~GlamNPCPack/Script/bgee~ ~^.+\.bcs$~ BEGIN // if a script in GlamNPCPack/Script/bgee isn't in the game, copy it over
  ACTION_IF !FILE_EXISTS_IN_GAME ~%BASH_FOR_FILE%~ BEGIN
    COPY ~%BASH_FOR_FILESPEC%~ ~override~
  END
END  

EXTEND_TOP ~%Beregost_RedSheaf_L1_BCS%.bcs~         ~GlamNPCPack/Script/fw3357.baf~ EVALUATE_BUFFER
EXTEND_TOP ~%Beregost_BCS%.bcs~                     ~GlamNPCPack/Script/fw3300.baf~ EVALUATE_BUFFER
EXTEND_TOP ~%HighHedge_House_BCS%.bcs~              ~GlamNPCPack/Script/fw3201.baf~ EVALUATE_BUFFER
EXTEND_TOP ~%Nashkel_BelchingDragonTavern_BCS%.bcs~ ~GlamNPCPack/Script/fw4809.baf~ EVALUATE_BUFFER
EXTEND_TOP ~%FriendlyArmInn_L1_BCS%.bcs~            ~GlamNPCPack/Script/fw2301.baf~ EVALUATE_BUFFER
EXTEND_TOP ~%BearRiver_BCS%.bcs~                    ~GlamNPCPack/Script/fw4600.baf~ EVALUATE_BUFFER
EXTEND_TOP ~%CloakwoodLodge_BCS%.bcs~               ~GlamNPCPack/Script/fw2200.baf~ EVALUATE_BUFFER
EXTEND_TOP ~%Beregost_BurningWizard_L1_BCS%.bcs~    ~GlamNPCPack/Script/fw3307.baf~ EVALUATE_BUFFER
EXTEND_TOP ~%DurlagsTower_D1_BCS%.bcs~              ~GlamNPCPack/Script/fw0511.baf~

// Creatures
COPY_EXISTING ~%tutu_var%FFHUNT.cre~ ~override/GV#MERI.cre~
  SAY NAME1 @149 /* Officer Meriwether */
  SAY NAME2 @149 /* Officer Meriwether */
  WRITE_ASCII 0x248 ~GV#MERI~ #8  //override script
  WRITE_ASCII 0x280 ~GV#MERI~ #32  //DV
  WRITE_ASCII 0x2cc ~GV#MERI~ #8   //pre-joining dialogue file

COPY_EXISTING ~%tutu_var%FTOBE5.cre~ ~override/GV#COMM1.cre~
  SAY NAME1 @150 /* Commoner */
  SAY NAME2 @150 /* Commoner */
  WRITE_ASCII 0x248 ~GV#COMM1~ #8  //override script
  WRITE_ASCII 0x280 ~GV#COMM1~ #32  //DV
  WRITE_ASCII 0x2cc ~GV#COMM1~ #8   //pre-joining dialogue file
  
COPY_EXISTING ~%tutu_var%MTOB5.cre~ ~override/GV#COMM2.cre~
  SAY NAME1 @150 /* Commoner */
  SAY NAME2 @150 /* Commoner */
  WRITE_ASCII 0x248 ~GV#COMM2~ #8  //override script
  WRITE_ASCII 0x280 ~GV#COMM2~ #32  //DV
  WRITE_ASCII 0x2cc ~GV#COMM2~ #8   //pre-joining dialogue file

COPY_EXISTING ~%tutu_var%MTOB6.cre~ ~override/GV#COMM3.cre~
  SAY NAME1 @150 /* Commoner */
  SAY NAME2 @150 /* Commoner */
  WRITE_ASCII 0x248 ~GV#COMM3~ #8  //override script
  WRITE_ASCII 0x280 ~GV#COMM3~ #32  //DV
  WRITE_ASCII 0x2cc ~GV#COMM3~ #8   //pre-joining dialogue file

LAF cd_portrait_npc STR_VAR source = ~dave~   destination = ~gv#dave~ source_path = ~glamnpcpack/portrait~ END
LAF cd_portrait_npc STR_VAR source = ~flara~  destination = ~gv#flar~ source_path = ~glamnpcpack/portrait~ END
LAF cd_portrait_npc STR_VAR source = ~littlu~ destination = ~gv#litt~ source_path = ~glamnpcpack/portrait~ END
LAF cd_portrait_npc STR_VAR source = ~moidre~ destination = ~gv#moid~ source_path = ~glamnpcpack/portrait~ END
LAF cd_portrait_npc STR_VAR source = ~vynd~   destination = ~gv#vynd~ source_path = ~glamnpcpack/portrait~ END



//ITEMS

COPY ~GlamNPCPack/Item/GV#BOOT.itm~ ~override/GV#BOOT.itm~
  SAY NAME1 @151 /* Courier's Boots */
  SAY NAME2 @151 /* Courier's Boots */
  SAY DESC @152

COPY ~GlamNPCPack/Item/GV#IDOL.itm~ ~override/GV#IDOL.itm~
  SAY NAME1 @153 /* Charged Idol */
  SAY NAME2 @153 /* Charged Idol */
  SAY DESC @154

COPY ~GlamNPCPack/Item/GV#THUN.itm~ ~override/GV#THUN.itm~
  SAY NAME1 @64 /* Thundercat */
  SAY NAME2 @64 /* Thundercat */
  SAY DESC @155

COPY ~GlamNPCPack/Item/GV#PARC.itm~ ~override/GV#PARC.itm~
  SAY NAME1 @156 /* Talassan Parchments */
  SAY NAME2 @156 /* Talassan Parchments */
  SAY DESC @157

COPY ~GlamNPCPack/Item/GV#SW1H.itm~ ~override/GV#SW1H.itm~
  SAY NAME1 @158 /* Bastard Sword */
  SAY NAME2 @159 /* Pam's Bastard Sword */
  SAY DESC @160

COPY ~GlamNPCPack/Item/GV#SHLD.itm~ ~override/GV#SHLD.itm~
  SAY NAME1 @161 /* Tower Shield */
  SAY NAME2 @161 /* Tower Shield */
  SAY DESC @162

COPY ~GlamNPCPack/Item/GV#WAND.itm~ ~override/GV#WAND.itm~
  SAY NAME1 @163 /* Drakonophobe Wand */
  SAY NAME2 @163 /* Drakonophobe Wand */
  SAY DESC @164

COPY ~GlamNPCPack/Item/GV#NAGA.itm~ ~override/GV#NAGA.itm~
  SAY NAME1 @165 /* Leather Armour */
  SAY NAME2 @166 /* Nagahide Jerkin */
  SAY DESC @167

COPY ~GlamNPCPack/Item/GV#RUBY.itm~ ~override/GV#RUBY.itm~
  SAY NAME1 @168 /* Dragon Statuette */
  SAY NAME2 @168 /* Dragon Statuette */
  SAY UNIDENTIFIED_DESC @169 /* This sculpture, carved from red crystal, emits a faint glow from its core. */
  SAY DESC @170

COPY ~GlamNPCPack/Item/GV#COIN.itm~ ~override/GV#COIN.itm~
  SAY NAME1 @171 /* Coin */
  SAY NAME2 @172 /* Unlucky Coin */
  SAY UNIDENTIFIED_DESC @173 /* This coin is of no denomination that you recognise */
  SAY DESC @174

COPY ~GlamNPCPack/Item/GV#CLCK.itm~ ~override/GV#CLCK.itm~
  SAY NAME1 @175 /* Cloak */
  SAY NAME2 @176 /* Owlbear Pelt */
  SAY DESC @177

COPY ~GlamNPCPack/Item/GV#STAF.itm~ ~override/GV#STAF.itm~
SAY NAME1 @178 /* Quarterstaff */
SAY NAME2 @179 /* Last Resort */
SAY DESC @180

COPY ~GlamNPCPack/Item/GV#XBOW.itm~ ~override/GV#XBOW.itm~
  SAY NAME1 @181 /* Plunkett Repeater */
  SAY NAME2 @181 /* Plunkett Repeater */
  SAY DESC @182

COPY ~GlamNPCPack/Item/GV#XBOW2.itm~ ~override/GV#XBOW2.itm~
  SAY NAME1 @183 /* Plunkett Reiterator */
  SAY NAME2 @183 /* Plunkett Reiterator */
  SAY DESC @184

COPY ~GlamNPCPack/Item/GV#XBOW3.itm~ ~override/GV#XBOW3.itm~
  SAY NAME1 @185 /* Heavy Crossbow */
  SAY NAME2 @186 /* Plunkett Goblinstopper */
  SAY DESC @187

COPY ~GlamNPCPack/Item/GV#MANU.itm~ ~override/GV#MANU.itm~
  SAY NAME1 @188 /* The Progress of Projectiles */
  SAY NAME2 @188 /* The Progress of Projectiles */
  SAY DESC @189

COPY ~GlamNPCPack/Item/GV#RING.itm~ ~override/GV#RING.itm~
  SAY NAME1 @190 /* Wedding Ring */
  SAY NAME2 @190 /* Wedding Ring */
  SAY DESC @191 /* A simple gold band, unadorned, unenchanted, and slightly tarnished on the inside from decades of wear. */

COPY ~GlamNPCPack/Item/GV#BOOK.itm~ ~override/GV#BOOK.itm~
  SAY NAME1 @192 /* Lockpicking for Total Muttonheads */
  SAY NAME2 @192 /* Lockpicking for Total Muttonheads */
  SAY 0x50 @193 /* you're crap you're crap you're crapyou're crap you're crap youre crap you're crap you're crap you're crap keep the pick level you dolt you're crap */
  SAY 0x54 @193 /* you're crap you're crap you're crapyou're crap you're crap youre crap you're crap you're crap you're crap keep the pick level you dolt you're crap */

COPY ~GlamNPCPack/Item/GV#HELM.itm~ ~override/GV#HELM.itm~

COPY ~GlamNPCPack/Item/GV#DIAR.itm~ ~override/GV#DIAR.itm~
  SAY NAME1 @194 /* Jeff's Old Journal */
  SAY NAME2 @194 /* Jeff's Old Journal */
  SAY DESC @195




//Spells
COPY ~GlamNPCPack/Spells/GV#LIGH.spl~ ~override/GV#LIGH.spl~
COPY ~GlamNPCPack/Spells/GV#STRE.spl~ ~override/GV#STRE.spl~
COPY ~GlamNPCPack/Spells/GV#CHAR.spl~ ~override/GV#CHAR.spl~
COPY ~GlamNPCPack/Spells/GV#BLAST.spl~ ~override/GV#BLAST.spl~
COPY ~GlamNPCPack/Spells/GV#SHOT.spl~ ~override/GV#SHOT.spl~
COPY ~GlamNPCPack/Spells/GV#SHOT2.spl~ ~override/GV#SHOT2.spl~
COPY ~GlamNPCPack/Spells/GV#XBOW.spl~ ~override/GV#XBOW.spl~
COPY ~GlamNPCPack/Spells/GV#PSN.spl~ ~override/GV#PSN.spl~
COPY ~GlamNPCPack/Spells/GV#DART.spl~ ~override/GV#DART.spl~
  SAY NAME1 @196 /* Poison Dart */

ACTION_IF !enhanced_edition BEGIN // do this via op360 on EEs
  COPY_EXISTING ~dplayer2.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN 
      REPLACE_TEXTUALLY ~!HappinessGT(Myself,-299)~
                        ~!HappinessGT(Myself,-299) !Name("GV#DAVE",Myself) !Name("GV#FLAR",Myself) !Name("GV#MOID",Myself) !Name("GV#VYND",Myself)~
      REPLACE_TEXTUALLY ~BreakingPoint()~
                        ~BreakingPoint() !Name("GV#DAVE",Myself) !Name("GV#FLAR",Myself) !Name("GV#MOID",Myself) !Name("GV#VYND",Myself)~
    END
END



APPEND ~pdialog.2da~
~GV#DAVE GV#DAVEP GV#DAVEJ GV#DAVED ~
UNLESS ~GV#DAVE~

APPEND ~pdialog.2da~
~GV#FLAR GV#FLARP GV#FLARJ GV#FLARD ~
UNLESS ~GV#FLAR~

APPEND ~pdialog.2da~
~GV#LITT GV#LITTP GV#LITTJ GV#LITTD ~
UNLESS ~GV#LITT~

APPEND ~pdialog.2da~
~GV#MOID GV#MOIDP GV#MOIDJ GV#MOIDD ~
UNLESS ~GV#MOID~

APPEND ~pdialog.2da~
~GV#VYND GV#VYNDP GV#VYNDJ GV#VYNDD ~
UNLESS ~GV#VYND~

APPEND ~interdia.2da~
~GV#DAVE BGV#DAVE~
UNLESS ~GV#DAVE~

APPEND ~interdia.2da~
~GV#FLAR BGV#FLAR~
UNLESS ~GV#FLAR~

APPEND ~interdia.2da~
~GV#LITT BGV#LITT~
UNLESS ~GV#LITT~

APPEND ~interdia.2da~ 
~GV#MOID BGV#MOID~
UNLESS ~GV#MOID~

APPEND ~interdia.2da~
~GV#VYND BGV#VYND~
UNLESS ~GV#VYND~

APPEND ~pdialog.2da~
~GV#ALAN~
UNLESS ~GV#ALAN~

APPEND ~pdialog.2da~
~GV#BRIA~
UNLESS ~GV#BRIA~

APPEND ~pdialog.2da~
~GV#JEFF~
UNLESS ~GV#JEFF~

APPEND ~pdialog.2da~
~GV#KELL~
UNLESS ~GV#KELL~

APPEND ~pdialog.2da~
~GV#PAM~
UNLESS ~GV#PAM~

APPEND ~pdialog.2da~
~GV#PHIL~
UNLESS ~GV#PHIL~

APPEND ~pdialog.2da~
~GV#THUN~
UNLESS ~GV#THUN~

APPEND ~pdialog.2da~
~GV#SIGU~
UNLESS ~GV#SIGU~

APPEND ~pdialog.2da~
~GV#KRAK~
UNLESS ~GV#KRAK~

APPEND ~pdialog.2da~
~GV#MARI~
UNLESS ~GV#MARI~

APPEND ~pdialog.2da~
~GV#WINK~
UNLESS ~GV#WINK~

APPEND ~pdialog.2da~
~GV#PIER~
UNLESS ~GV#PIER~

APPEND ~pdialog.2da~
~thalan~
UNLESS ~%tutu_var%THALAN~



